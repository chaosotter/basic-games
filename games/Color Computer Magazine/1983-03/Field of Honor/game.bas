10 CLS:CLEAR1000:DIM MM(6,5)
20 A$="T4O2L4GL2B-O3L4CL4.DL8EL4DL2CO2L4AL4.FL8GL4AL2B-L4GP32L4.GL8FL4GL2AL4FL2D"
30 B$="T4O2L4GL2B-O3L4CL4.DL8EL4DL2CO2L4AL4.FL8GL4AL4B-AGF+EF+L2.GP32L2GP4"
40 C$="T4O3L2.FP32L4.FL8EL4DL2CO2L4AL4.FL8GL4AL2B-L4GP32L4.GL8FL4GL2AL4FL2DP4"
50 D$="T4O3L2.FP32L4.FL8EL4DL2CO2L4AL4.FL8GL4AB-AGF+EF+L2.GP32L2G"
60 PRINT@135,"FIELD OF HONOUR":PLAY"XA$;"
70 PRINT@193,"A MEDIEVAL JOUSTING TOURNAMENT":PLAY"XB$;"
80 PRINT@262,"BY SCOTT L. NORMAN":PLAY"XC$;"
90 PRINT@327,"NOVEMBER, 1982":PLAY"XD$;"
100 FOR L=488 TO 497:PRINT@L,CHR$(128);:NEXT
110 FOR L=456TO465:PRINT@L,CHR$(128);:NEXT:PRINT@466,CHR$(129);
120 PRINT@424,CHR$(128)+CHR$(128);:FORL=426TO432:PRINT@L,CHR$(136);:NEXT:PRINT@433,CHR$(128)+CHR$(128);
130 PRINT@392,CHR$(132)+CHR$(136);:PRINT@400,CHR$(142)+CHR$(128)+CHR$(132);
140 PRINT@369,CHR$(142);
150 PRINT@424,CHR$(148);:PRINT@457,CHR$(148);
160 PRINT@401,CHR$(145);:PRINT@433,CHR$(145);
170 FORT=1TO1500:NEXT
180 CLS:PRINT@11,"**BEWARE**":PRINT
190 PRINT"NONE MAY STEP UPON THE JOUSTING FIELD WHO HAVE NOT UTTERED THE  MAGIC INCANTATION.  IF THOU     HAST, THEN PRESS YE SPACEBAR; IFTHOU HAST NOT, ONLY THE RED     <BREAK> KEY WILL SUFFICE.":PRINT
200 PRINT"(IN OTHER WORDS, ENTER 'PCLEAR6'BEFORE RUNNING THIS GAME, OR THEGODS WILL CAUSE THE MACHINE TO  SHUT DOWN ON THEE.)":PRINT
210 IFINKEY$<>" "THEN210
220 GOTO760
230 IF BN>1 THEN590ELSEFORP=1TO5STEP2
240 PMODE1,P:PCLS1
250 U=136-8*P:Y$=STR$(U)
260 DRAW"S4;BM178,"+Y$+"U30R60D30"
270 CIRCLE(208,U),30,0,2,0,.5
280 ONA GOTO290,330,360,390
290 DRAW"BM-60,-30F56"
300 DRAW"BM-56,-36F50"
310 PAINT(208,U-4),2,0:PAINT(208,U+4),4,0:PAINT(208,U+24),2,0
320 GOTO410
330 DRAW"BM-60,-30F30E30":DRAW"BM+0,+30G28H28"
340 PAINT(208,U-4),3,0:PAINT(208,U+4),2,0:PAINT(208,U+32),3,0
350 GOTO410
360 DRAW"BM-60,-30F56BL52E56"
370 PAINT(208,U-4),3,0:PAINT(208,U+4),3,0:PAINT(204,U),1,0:PAINT(212,U),2,0
380 GOTO410
390 PAINT(208,U),4,0
400 GOTO410
410 Y$=STR$(104-6*P)
420 IFA=4 THEN450
430 DRAW"BM178,"+Y$+"L12H4U36H4L32G4D36G4L24G4D44F4R12E4U16D40G20D32"
440 GOTO480
450 DRAW"BM178,"+Y$+"L12H4U36L40R40M-2,-6;L36R36H18G18M-2,6;D36G4L24G4D44F4R12E4U16D40G20D32"
460 PAINT(142,63-6*P),2,0
470 PAINT(142,52-6*P),2,0
480 Y$=STR$(136-6*P)
490 DRAW"BM178,"+Y$+"L4D40F20D32"
500 PAINT(140,140),2,0
510 Y$=STR$(128-4*P)
520 DRAW"BM140,"+Y$+"R16U12F8D16F4D16G12D20G4L12":DRAW"BM140,"+Y$+"L14U12G8D16G4D16F12D20F4R10"
530 CIRCLE(100,U),12,0:PAINT(100,U),3,0:CIRCLE(100,U),4,2
540 Y$=STR$(84-6*P):DRAW"BM162,"+Y$+"L40U10R10D10U10R10D10U10R10D10U10R10D10U10"
550 Y$=STR$(104-6*P):DRAW"BM170,"+Y$+"G8L40H8"
560 CIRCLE(168,148-4*P),4:CIRCLE(114,148-4*P),4
570 DRAW"BM4,36E16U12D12F16D28L32U28":PAINT(20,24),3,0:DRAW"BM36,36E8U6D6F8D14L16":PAINT(44,36),2,0
580 NEXT
590 CLS:PRINT@167,"GOOD LUCK, M'LORD!":PRINT@231,"PRESS <C> TO CHARGE"
600 IFINKEY$<>"C"THEN600
610 PMODE1,1:SCREEN1,0
620 M$="T2L16O3AL4O4C":PLAY"XM$;XM$;"
630 FORT=1TO500:NEXT
640 Z=1
650 FORP=1TO5STEP2
660 GOSUB730
670 NEXT
680 PLAY"L32O2CE"
690 FORP=5TO1STEP-2
700 GOSUB730
710 NEXT
720 Z=Z+1:IFZ<9THEN650ELSERETURN
730 PMODE1,P:SCREEN1,0
740 FORT=1TO10*P/Z:NEXT
750 RETURN
760 CLS:PRINT"   YOU ARE A MEDIEVAL KNIGHT"
770 PRINT"IN A JOUSTING TOURNAMENT, HOPINGTO WIN THE PRINCESS'S HAND IN   MARRIAGE."
780 PRINT"   TO WIN, YOU MUST DEFEAT FOUR OTHER KNIGHTS IN BATTLE."
790 PRINT"   BEWARE!  YOUR OPPONENTS WILL BECOME TOUGHER AND MORE CRAFTY  AS YOU ADVANCE IN THE COMBAT."
800 FORB=1 TO6
810 FORC=1 TO5
820 READ MM(B,C)
830 NEXTC
840 NEXTB
850 RESTORE
860 GOSUB1890
870 CLS
880 FORA=1TO4
890 ONA GOTO900,910,920,930
900 PRINT"  THIS IS YOUR FIRST JOUST.     YOU ARE UP AGAINST SIR FREDERICKTHE FILTHY.":GOTO940
910 PRINT"  THIS IS YOUR SECOND JOUST.    YOUR FOE: LORD CLIVE THE CREEP.":GOTO940
920 PRINT"  YOU ARE DOING WELL!  YOUR     THIRD BOUT IS AGAINST BARON     RUDOLPH THE RAT.":GOTO940
930 PRINT"THIS IS YOUR FINAL TEST.  IF YOUWIN THIS TIME, THE PRINCESS IS  YOURS!  YOUR OPPONENT IS THE    DREADED, FIERCE, RED KNIGHT."
940 PRINT"  YOU MAY HONOURABLY AIM AT THE:"
950 PRINT"1) HELMET":PRINT"2) BREASTPLATE"
960 PRINT"3) UPPER LEFT OF SHIELD":PRINT"4) UPPER RIGHT OF SHIELD"
970 PRINT"5) LOWER LEFT OF SHIELD":PRINT"6) LOWER RIGHT OF SHIELD"
980 PRINT:INPUT"YOUR AIMING POINT (1-6)";B
990 IFB<1ORB>6THEN980
1000 CLS
1010 PRINT"HOW WILL YOU DEFEND YOURSELF?"
1020 PRINT:PRINT"1) LOWER HEAD AND CHARGE":PRINT"2) SHIELD HELD HIGH"
1030 PRINT"3) SHIELD HELD LOW":PRINT"4) LEAN TOWARD YOUR FOE"
1040 PRINT"5) LEAN AWAY FROM YOUR FOE"
1050 PRINT:INPUT"WHAT IS YOUR CHOICE";C:BN=BN+1
1060 IFBN>1THEN1140
1070 R=RND(-TIMER):R=RND(5):CLS
1080 ON R GOTO 1090,1100,1110,1120,1130
1090 PRINT:PRINT"  I WILL SEE TO YOUR HORSE AND  ARMOUR; WAIT HERE.":GOTO1140
1100 PRINT:PRINT"  YE HAD BETTER REST HERE WHILE I SEE TO YOUR THINGS.  THIS     KNIGHT MAY BE TOUGHER THAN YE   THINK!":GOTO1140
1110 PRINT:PRINT"  YOU CAN'T FIGHT IN ARMOUR AS  BATTERED AS THAT!  'TIS A DIS-  GRACE!  WAIT WHILE I REPAIR IT.":GOTO1140
1120 PRINT:PRINT"  THE BIG BUFFOON HASN'T LEFT   HIS TENT YET.  HE MUST BE SORE  AFRAID OF YE, SIRE!  I'LL GO ANDFETCH THE LOUT.":GOTO1140
1130 PRINT:PRINT"  YE HAD BEST USE THIS MOMENT   TO STEEL YOURSELF FOR COMBAT,   MY LORD.  YOUR FOE MAY BE AN    OAF, BUT I HEAR TELL HE CAN     FIGHT!"
1140 GOSUB230
1150 SCREEN0,0:FORW=1TO3
1160 CLS4:FORT=1TO75:NEXT
1170 CLS2:FORT=1TO75:NEXT
1180 NEXT
1190 CLS:R=RND(TIMER):R=RND(2)-1
1200 SC=MM(B,C)*SQR(5-A)+R
1210 IF SC<3 THEN1260
1220 ' TIES OR WINS
1230 D=RND(-TIMER):D=RND(4)
1240 ON D GOTO 1280,1300,1320,1340
1250 ' LOSSES
1260 D=RND(-TIMER):D=RND(4)
1270 ON D GOTO 1360,1380,1400,1420
1280 PRINT"HE MISSED YOU!"
1290 S=0:GOTO1440
1300 PRINT"HE HIT YOUR SHIELD BUT HIS BLOW GLANCED OFF."
1310 S=0:GOTO1440
1320 PRINT"HE KNOCKED OFF YOUR HELM!!"
1330 S=3:GOTO1440
1340 PRINT"HE BROKE HIS LANCE."
1350 S=0:GOTO1440
1360 PRINT"HE HAS UNSEATED YOU (THUD)!"
1370 S=5:GOTO1440
1380 PRINT"HE HAS BROKEN HIS LANCE, INJUREDAND UNSEATED YOU (OUCH)!"
1390 S=5:GOTO1440
1400 PRINT"HE HAS INJURED AND UNSEATED YOU (CRASH)!"
1410 S=5:GOTO1440
1420 PRINT"HE HAS BROKEN HIS LANCE AND UN- SEATED YOU(CLANG)!"
1430 S=5
1440 PRINT
1450 IF SC>=6 THEN 1500
1460 ' TIES OR LOSSES
1470 E=RND(-TIMER):E=RND(4)
1480 ON E GOTO 1520,1540,1560,1580
1490 ' WINS
1500 E=RND(-TIMER):E=RND(4)
1510 ON E GOTO 1600,1620,1640,1660
1520 PRINT"YOU MISSED HIM (HISS)!"
1530 T=0:GOTO1680
1540 PRINT"YOU HIT HIS SHIELD BUT YOUR BLOWGLANCED OFF."
1550 T=0:GOTO1680
1560 PRINT"YOU KNOCKED OFF HIS HELM        (LOUD CHEERS)!"
1570 T=3:GOTO1680
1580 PRINT"YOU BROKE YOUR LANCE (CRACK...)"
1590 T=0:GOTO1680
1600 PRINT"YOU UNSEATED HIM (LOUD CHEERS   AND HUZZAHS)!"
1610 T=5:GOTO1680
1620 PRINT"YOU BROKE YOUR LANCE, BUT UN-   SEATED AND INJURED YOUR FOE!"
1630 T=5:GOTO1680
1640 PRINT"YOU HAVE INJURED AND UNSEATED   YOUR OPPONENT!"
1650 T=5:GOTO1680
1660 PRINT"YOU BROKE YOUR LANCE BUT UNSEAT-ED YOUR OPPONENT."
1670 T=5:GOTO1680
1680 IFS=T ANDS=0THEN1850
1690 IFS=T THEN1820
1700 IFS<T THEN1720
1710 IFS>T THEN1790
1720 PRINT
1730 PRINT"SIR, YOU HAVE WON THIS JOUST!":PLAY"L16O3AL4O4C"
1740 PRINT
1750 IFA=4THEN1960
1760 GOSUB1890
1770 BN=0:NEXTA
1780 GOTO1950
1790 PRINT
1800 PRINT"TOO BAD, YOU LOST! HOPE YOUR IN-SURANCE WAS PAID UP.":PLAY"O1L1C"
1810 GOTO1920
1820 PRINT
1830 PRINT"TOO BAD, YOU HAVE BOTH LOST.  ATLEAST YOUR HONOUR IS INTACT.":PLAY"L16O3AL4O4CP32L16CL4O3A"
1840 GOTO1920
1850 PRINT
1860 PRINT"YOU MAY TRY AGAIN.":PLAY"L16O4CL4O3A"
1870 GOSUB1890
1880 GOTO950
1890 PRINT@448,"PRESS ANY KEY TO CONTINUE"
1900 IF INKEY$=""THEN1900
1910 CLS:RETURN
1920 PRINT
1930 PRINT"SORRY, BETTER LUCK NEXT JOUST!"
1940 GOTO2170
1950 PRINT
1960 PRINT"HOORAY!  YOU ARE THE VICTOR!!":PRINT:PLAY"XM$;XM$;XM$;"
1970 CLS:PRINT:PRINT"NOW THE KING WILL REWARD YOU    WITH ONE OF HIS DAUGHTERS' HANDSIN MARRIAGE."
1980 PRINT:PRINT"  WHAT'S THAT?  YOU DIDN'T KNOW THAT HE HAS TWO DAUGHTERS?  AH  WELL, HE DOES.  I WONDER WHICH  YOU SHALL MARRY."
1990 PRINT:PRINT"  BE PATIENT, SIRE.  IT ALWAYS  TAKES THESE WOMEN FOREVER TO    PREPARE."
2000 FORT=1TO3500:NEXT
2010 PMODE3,1:PCLS5:COLOR6,5
2020 DRAW"S8;BM124,72;M+4,-3;M+4,-1;R8M+4,1;M+4,3;M+2,4;M-2,4;M+2,8;M-3,8;M-2,2;M-4,2;L10H2;M-6,-2;M-2,-4;U6E2U4H2U2E4":' HEAD
2030 R=RND(-TIMER):R=RND(2)
2040 ON R GOTO2050,2100
2050 DRAW"BM+14,10;M+2,8;L4":DRAW"BM-5,3;R6D1M+4,-1;M+2,1;M-3,2;L5M-5,-3;":PAINT(150,117),7,6:' NOSE, LIPS
2060 CIRCLE(136,92),4,6:CIRCLE(164,92),4,6:PAINT(136,92),6,6:PAINT(164,92),6,6:CIRCLE(140,92),16,8,.5,.6,.8:CIRCLE(168,92),16,8,.5,.6,.8
2070 CIRCLE(146,96),44,6,1.1,.5,1:CIRCLE(254,96),64,6,1,.4,.5:CIRCLE(214,96),40,6,1,.3,.5:CIRCLE(10,96),92,6,1,0,.11:CIRCLE(60,96),62,6,1,0,.2:PAINT(146,56),8,6:' HAIR
2080 DRAW"BM146,48;M-40,-15;M+18,35"
2090 GOTO2140
2100 DRAW"BM+12,6;F6M+2,4;D2M-6,-4;":CIRCLE(152,116),12,7,.5,.5,1:' NOSE, LIPS
2110 CIRCLE(140,88),4,6:CIRCLE(164,88),4,6:PAINT(140,88),6,6:PAINT(164,88),6,6:CIRCLE(134,88),16,8,.5,.7,.9:CIRCLE(168,88),16,8,.5,.6,.8:' EYES
2120 CIRCLE(146,96),44,6,1.1,.5,1:DRAW"BM102,96;R10;":DRAW"BM190,96;L8;":PAINT(146,56),8,6:DRAW"C8;BM102,96;R10D16BL4BU4U12L2D16BL2BU6U10;":DRAW"C8;BM190,96;L8;BR2D14BR2U16D16BR3BD2U18;"
2130 DRAW"C6;BM146,48;M-32,-12;G8M+7,-5;M+12,24"
2140 CLS:PRINT:PRINT"  AHA--":PRINT"HERE COMES THE BRIDE!":FORT=1TO1000:NEXT
2150 SCREEN1,1
2160 PLAY"T1O2L4CL8.FP32L16FP32L4F":FORT=1TO1500:NEXT
2170 CLS:PRINT:INPUT "DO YOU WANT TO FIGHT AGAIN";AN$
2180 IFLEFT$(AN$,1)="Y" THEN BN=0ELSE2200
2190 GOTO220
2200 IFLEFT$(AN$,1)<>"N"THEN2170
2210 CLS:PRINT:PRINT"YOU BLOODY COWARD!!"
2220 PRINT:PRINT"ARE YOU AFRAID OF A LITTLE PAIN AND DEATH?"
2230 PRINT:PRINTTAB(7)"**FIE ON YOU!**":PLAY"O1L1C"
2240 GOTO2240
2250 DATA 1,4,4,2,1
2260 DATA 3,5,4,5,4
2270 DATA 2,4,3,2,1
2280 DATA 2,4,3,3,2
2290 DATA 5,3,4,2,1
2300 DATA 5,3,4,3,2
