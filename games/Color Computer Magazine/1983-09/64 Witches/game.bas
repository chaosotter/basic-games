
100 REM* 64 WITCHES * 16K COLOR
105 REM* SORCERER'S PUZZLES #3 / R. RAMELLA
110 CLS0:CLEAR500:VV=1:FORA=1 TO 15:A$=A$+CHR$(128):NEXT:FORA=1 TO 8:A$(A)=A$:NEXT:U$=CHR$(94)
120 D$=CHR$(10):L$=CHR$(8):R$=CHR$(9):Y=0:FORX=30 TO 63:SET(X,0,8):SET(X,19,8):NEXT:FORY=0TO 19
130 SET(30,Y,8):SET(63,Y,8):NEXT:PRINT@0,"64 WITCHES ";:X=48:M=1:P=1:C=16:PRINT@X,CHR$(143+C);
140 Z$=INKEY$:PRINT@480,"LEVEL"VV;CHR$(143+C)" SCORE:"S;:IF QQ=8 THEN C=C+16:VV=VV+1:QQ=0
150 IF Z$=D$ THEN X=X+32: B=B+2: P=P+1
160 IF Z$=U$ THEN X=X-32: B=B-2: P=P-1
170 IF Z$=L$ THEN X=X-2: A=A-4: M=M-2
180 IF Z$=R$ THEN X=X+2: A=A+4: M=M+2
190 IF (X-14)/32 = INT((X-14)/32) THEN X=X+2: A=A+4: M=M+2
200 IF X/32 = INT(X/32) THEN X=X-2: A=A-4: M=M-2
210 IF X<47 THEN X=X+32: B=B+2: P=P+1
220 IF X>287 THEN X=X-32: B=B-2: P=P-1
230 Q=1:FOR N=48 TO 272 STEP32:PRINT@N,A$(Q);:Q=Q+1:NEXT:PRINT@X,CHR$(143+C);:FOR T=1 TO 20:NEXTT:PRINT@X,CHR$(128);
240 FORT=1 TO 10:NEXTT:PRINT@X,CHR$(143+C);:IF Z$="S" GOSUB260 ELSEIF Z$="E" GOSUB420 ELSEIF Z$="T" GOTO450
250 GOTO140
260 IF MID$(A$(P),M,1) <> CHR$(128) THENRETURN ELSEA$(P)=LEFT$(A$(P),M-1)+CHR$(143+C)+RIGHT$(A$(P),LEN(A$(P))-M):QQ=QQ+1:S=S+1
270 FORZ=1 TO 15 STEP2:IF MID$(A$(P),Z,1)=CHR$(143+C) THENF=F+1
280 IF F=2 THEN F=0: GOSUB440: RETURN
290 NEXT:F=0:FORZ=1TO8:IF MID$(A$(Z),M,1)=CHR$(143+C) THENF=F+1
300 IF F=2 THEN F=0: GOSUB440: RETURN
310 NEXTZ:F=0:L=P:Z=M
320 IF L=1 OR Z=1 GOTO330 ELSE L=L-1: Z=Z-2: GOTO320
330 FORL=L TO L+8:IF MID$(A$(L),Z,1)=CHR$(143+C) THENF=F+1
340 IF F=2 THEN F=0: GOSUB440: RETURN
350 IF L=8 OR Z=15 GOTO360 ELSEZ=Z+2: NEXTL
360 F=0:L=P:Z=M
370 IF L=1 OR Z=15 GOTO380 ELSEL=L-1: Z=Z+2: GOTO370
380 FORL=L TOL+8:IF MID$(A$(L),Z,1)=CHR$(143+C) THENF=F+1
390 IF F=2 THEN F=0: GOSUB440: RETURN
400 IF L=8OR Z=1 THENF=0:RETURN
410 Z=Z-2:NEXTL:F=0:RETURN
420 IF MID$(A$(P),M,1)=CHR$(143+C) THEN A$(P)=LEFT$(A$(P),M-1) + CHR$(128) + RIGHT$(A$(P),LEN(A$(P))-M) ELSERETURN
430 QQ=QQ-1:S=S-1:RETURN
440 FORT=1 TO 10:PRINT@X,CHR$(143+RND(7)*16);:SOUND 150,1:SOUND 200,1:NEXT:GOTO420
450 FORA=1 TO 8:FORZ=1 TO 15 STEP2:W$=MID$(A$(A),Z,1):IF W$=CHR$(143+16) THENW$="1" ELSEIF W$=CHR$(143+32) THENW$="2
460 IF W$=CHR$(143+48) THEN W$="3" ELSEIF W$=CHR$(143+64) THENW$="4" ELSEIF W$=CHR$(143+80) THENW$="5
470 IF W$=CHR$(143+96) THENW$="6" ELSEIF W$=CHR$(143+112) THENW$="7" ELSEIF W$=CHR$(128) THENW$="O
480 A$(A)=LEFT$(A$(A),Z-1) + W$ + RIGHT$(A$(A),LEN(A$(A))-Z):NEXTZ,A:A=1:FORN=48 TO 272 STEP32:PRINT@N,A$(A);:A=A+1:NEXT
490 PRINT@416,"FINAL SCORE"S;:GOTO490
