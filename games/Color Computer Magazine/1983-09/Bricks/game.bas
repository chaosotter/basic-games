
100 REM* BRICKS *COLOR BASIC 4K
110 REM* SORCERER'S PUZZLES #3 / R. RAMELLA
120 CLS 0
130 FOR A=1 TO 11
140 K$=K$+CHR$(128):NEXT
170 W=192:FOR A=1 TO 4
180 A$=A$+CHR$(143+112)
190 B$=B$+CHR$(143+48)
200 C$=C$+CHR$(143+80)
210 D$=D$+CHR$(143+64)
220 E$=E$+CHR$(143+32)
230 F$=F$+CHR$(128):NEXT
250 G$=A$+F$+B$
260 H$=C$+D$+E$
270 PRINT@33,G$;
280 PRINT@65,G$;
290 PRINT@97,H$;
300 PRINT@129,H$;
320 Y=1:FOR X=1 TO 25
330 SET(X,Y,1)
340 SET(X,Y+9,1):NEXT
370 X=1:FOR Y=1 TO 10
380 SET(X,Y,1)
390 SET(X+25,Y,1):NEXT
410 X=5:Y=4:V=33
440 Z$=INKEY$
450 PRINT@448,"MOVES MADE:"Q;
460 IF Q=25 THENPRINT@416,"25 MOVES UP. TRIAL MUST END.";:SOUND1,5:GOTO460
470 RESET(X,Y)
480 Z=POINT(X+1,Y)
490 FOR T=1 TO 10:NEXT T
510 IF Z<>0 GOTO 580
520 SET(X,Y,Z)
530 FOR T=1 TO 40:NEXT T
550 PRINT@V,F$;
560 PRINT@V+32,F$;
570 GOTO 610
580 FOR T=1 TO 10:NEXT T
600 SET(X,Y,Z)
610 IF Z$=CHR$(94) AND Y-4>3 THEN Y=Y-4:V=V-64
620 IF Z$=CHR$(10) AND Y+4<9 THEN Y=Y+4:V=V+64
630 IF Z$=CHR$(8) AND X-8>4 THEN X=X-8:V=V-4
640 IF Z$=CHR$(9) AND X+8<22 THEN X=X+8:V=V+4
650 P=0
660 IF Z$="M" GOSUB 680
670 GOTO 440
680 IF Z=8 THEN J$=A$:L$="O "
690 IF Z=4 THEN J$=B$:L$="R "
700 IF Z=6 THEN J$=C$:L$="C "
710 IF Z=5 THEN J$=D$:L$="W "
720 IF Z=3 THEN J$=E$:L$="B "
730 IF Z=0 GOSUB 1150:RETURN
740 IF X=5 AND Y=4 GOSUB 900
750 IF X=13 AND Y=4 GOSUB 930
760 IF X=21 AND Y=4 GOSUB 970
770 IF X=21 AND Y=8 GOSUB 1000
780 IF X=13 AND Y=8 GOSUB 1030
790 IF X=5 AND Y=8 GOSUB 1070
800 IF P=0 GOSUB 1150:RETURN
810 PRINT@V,F$;
820 PRINT@V+32,F$;
830 PRINT@P,J$;
840 PRINT@P+32,J$;
850 PRINT@W,L$;
860 W=W+LEN(L$)
870 Q=Q+1
880 IF POINT(4,3)=3 AND POINT(22,7)=8 GOTO 1100
890 RETURN
900 IFPOINT(X+8,Y)=0 THEN P=V+4
910 IFPOINT(X,Y+4)=0 THENP=B+64
920 RETURN
930 IFPOINT(X-8,Y)=0 THEN P=V-4
940 IFPOINT(X+8,Y)=0 THEN P=V+4
950 IFPOINT(X,Y+4)=0 THENP=V+64
960 RETURN
970 IFPOINT(X-8,Y)=0 THEN P=V-4
980 IFPOINT(X,Y+4)=0 THENP=V+64
990 RETURN
1000 IFPOINT(X-8,Y)=0 THENP=V-4
1010 IFPOINT(X,Y-4)=0THENP=V-64
1020 RETURN
1030 IFPOINT(X-8,Y)=0 THENP=V-4
1040 IFPOINT(X+8,Y)=0 THENP=V+4
1050 IFPOINT(X,Y-4)=0THENP=V-64
1060 RETURN
1070 IFPOINT(X,Y-4)=0THENP=V-64
1080 IFPOINT(X+8,Y)=0 THENP=V+4
1090 RETURN
1100 PRINT@448,"MOVES MADE:"Q;
1110 PRINT@480,"WINNER IN"Q"MOVES";
1120 SOUND 100,1
1130 SOUND 200,1
1140 GOTO 1120
1150 PRINT@17,"IMPOSSIBLE ";
1160 SOUND 100,5
1170 FORT=1 TO 500:NEXT
1190 PRINT@17,K$;
1200 RETURN
