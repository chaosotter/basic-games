
10 CLEAR 2000
20 GOSUB 1420
30 CLS:GOTO 930
40 REM **MOVEMENT**
50 FOR X=1TO25:IF E(X)=30+P THEN PRINT"YOU HAVE BEEN CONVICTED OF      REMOVING EVIDENCE FROM THE SCENEOF A CRIME. YOU ARE SENTENCED TOTHE LOSS OF THIS AND YOUR NEXT  TURN":E(X)=P1:T=4:T(P)=1:T$(P)="IN JAIL FOR REMOVING EVIDENCE":GOTO1280
60 NEXT X
70 ON N GOTO 110,150,190,230,270
80 IF NOUN$="UP"THEN 290
90 CLS:PRINT:PRINT"       ?????SAY WHAT?????":PRINT:GOTO1290
100 REM
110 IF P1=17 THEN P1=16:GOTO310
120 FOR X=1TO8
130 IF DN(X)=P1 THEN P1=P1-3:GOTO 310
140 NEXT X:GOTO1070
150 IF P1=16 THEN P1=17:GOTO 310
160 FOR X=1 TO 9
170 IF DS(X)=P1 THEN P1=P1+3:GOTO310
180 NEXT X:GOTO1070
190 IF P1=3 THEN P1=22 ELSE IF P1=9 OR P1=12 THEN P1=18:GOTO 310
200 FOR X=1TO15
210 IF DE(X)=P1 THEN P1=P1+1:GOTO 310
220 NEXT X:GOTO 1070
230 IF P1=10 THEN P1=1 ELSE IF P1=18 THEN P1=RND(20):GOTO 310
240 FOR X=1TO14
250 IF DW(X)=P1 THEN P1=P1-1:GOTO 310
260 NEXT X:GOTO 1070
270 IF P1=22 THEN P1=3 ELSE IF P1=14 THEN P1=17 ELSE IF P1=20 THEN P1=2 ELSE IF P1=24 THEN P1=20 ELSE GOTO 1070
280 GOTO 1060 
290 IF P1=22 THEN P1=19 ELSE IF P1=2THEN P1=20 ELSE IF P1=20 THEN P1=24 ELSE IF P1=17 THEN P1=14 ELSE GOTO 1070
300 GOTO 1060
310 IF RN>10 AND E(12)=P1 THEN PRINT"A DOG HAS BITTEN YOU! YOU LOSE  THIS AND YOUR NEXT TURN.":T(P)=1:T$(P)="STILL HEALING FROM DOG BITE":T=4:GOTO 1280
320 IF RN>10 AND E(25)=P1 THEN PRINT"A CAT HAS JUST SCRATCHED YOU!   YOU LOSE THIS TURN AND YOUR NEXTTURN.":T(P)=1:T$(P)=" TAKING CARE OF A CAT SCRATCH":T=4:GOTO1280
330 IF RN<4 AND P1=15 OR P1=23 THEN PRINT"YOU HAVE JUST WALKED IN ON A    YOUNG LADY USING THE BATHROOM!  YOU LOSE THE REST OF THIS TURN  OUT OF EMBARRASSMENT.":T=4:GOTO 1280
340 IF RN>19 THEN PRINT"A FLASH OF LIGHT BLINDS YOU FOR A MOMENT.":E(26)=P1:FOR X=1TO1500:NEXT X
350 GOTO1060
360 REM **PICK UP**
370 PRINT
380 IF N<5 OR N>LEN(NU$)/3 THEN PRINT"YOU CAN'T GET THAT!!!":GOTO 1290
390 N=N-5
400 IF E(N)=P1 AND N=21 THEN PRINTR$(N):GOTO1290
410 IF E(N)=P1 AND N=19 THEN PRINT"DON'T BE RIDICULUS-JUST EXAMINE IT!":GOTO 1290
420 IF E(N)=P1 THEN E(N)=30+P:PRINT"O.K., I'M HOLDING IT.":GOTO1290
430 PRINT"I DON'T KNOW WHAT YOU'RE        TALKING ABOUT- I DON'T SEE      "E$(N)".":GOTO 1290
440 REM ** DROP **
450 IF NOUN$="GUN"THEN PRINT"IT WOULD BE STUPID TO DROP YOUR GUN!":GOTO 1290
460 N=N-5:IF N>27OR N<1 THEN 530
470 IF N=27AND P1=18AND P=TI THENCLS:PRINT"OK-IT SANK TO THE BOTTOM AND I  CAN'T SEE IT":PRINT"CONGRATULATIONS. YOU HAVE BEEN  SUCCESSFUL AT HIDING YOUR       VICTIM'S BODY!":PRINT"THE CASE HAS BEEN THROWN OUT OF COURT DUE TO LACK OF EVIDENCE."ELSE 490
480 PRINT:PRINTG$(TI)" IS THE VICTOR!":GOTO 900
490 IF N=27 AND P1=18 THEN PRINT"YOU HAVE BEEN CONVICTED AS AN   ACCOMPLICE TO MURDER. YOU HAVE  BEEN SENTENCED TO THE LOSS OF   THIS AND THE NEXT THREE TURNS.":E(27)=24:T(P)=3:T=4:T$(P)="CONVICTED AS MURDER ACCOMPLICE":GOTO1280
500 IF E(N)=30+P THEN E(N)=P1:GOTO 520
510 GOTO 530
520 PRINT"O.K. I'VE DROPPED IT.":GOTO 1290
530 PRINT"I CAN'T DROP WHAT I DON'T HAVE!":GOTO 1290
540 REM *EXAMINE*
550 IF NOUN$="GUN"THEN PRINT"MFG.BY COLT":GOTO 1290
560 PRINT:N=N-5:IF N>27 OR N<1 THEN610
570 IF N=19 AND E(N)=P1 THEN PRINTR$(N):GOTO 1290
580 IF N=1 OR N=6 OR N=13 OR N=17 OR N=24 AND E(N)=P1 THEN PRINT "IT'S NOT A RABBIT":GOTO 1290
590 IF N=26 AND E(N)=30+P THEN T=T-2
600 IF E(N)=30+P THEN PRINT R$(N):E(26)=0:GOTO1290
610 PRINT"I DON'T HAVE IT!":GOTO1290
620 PRINT"i'm carrying:":PRINT"A GUN"
630 FOR X=1TO27:IF E(X)=30+P THEN PRINTE$(X)", ";
640 NEXT X:PRINT:GOTO 1290
650 PRINT:FOR X=1TO5
660 IF LEFT$(G$(X),3)=NOUN$ THEN 680 ELSE NEXT X
670 PRINT"THAT'S BEYOND DESCRIPTION.":GOTO1290
680 CLS:PRINT@43,G$(X):PRINTD$(X):PRINT@483,"PRESS <ENTER> TO CONTINUE"
690 X$=INKEY$:IF X$<>""THEN1070ELSE 690
700 CLS:FOR X=1TO5:PRINTX"-"G$(X)"-- "CL$(X,P):NEXT X
710 PRINT:INPUT"ADD CLUE(Y/N)";C$:IF C$="N"THEN 1290
720 IF C$<>"Y"THEN 710
730 INPUT "WHO(1-5)";X
740 PRINT "TYPE CLUE(SHORT) FOR "G$(X)".  ";:LINE INPUT CU$(X)
750 CL$(X,P)=CL$(X,P)+CU$(X)+","
760 GOTO700
770 PRINT:FOR X=1TO5
780 IF LEFT$(G$(X),3)=NOUN$ THEN 800 ELSE NEXT X
790 PRINT"FIND IT YOURSELF!!!":GOTO1290
800 PRINTG$(X)" IS IN THE "L$(G(X)):GOTO 1290
810 IF NOUN$="COR" THEN PRINT"THAT'S SICK!!!":GOTO 1290 ELSEFOR X=1TO5:IF LEFT$(G$(X),3)<>NOUN$ THEN NEXT X:PRINT"YOU CAN'T SHOOT THAT.":GOTO1290
820 IF G(X)<>P1 THEN PRINTG$(X)" IS NOT HERE!":GOTO 1290
830 IF X=P THEN PRINT"YOU HAVE INJURED YOURSELF- YOU  LOSE THIS AND YOUR NEXT TURN!":T(P)=1:T$(P)="   SHOULDN'T PLAY WITH GUNS":T=4:GOTO 1280
840 IF G$(X)=G$(TI) THEN CLS:PRINT"CONGRATULATIONS ON SEEING THAT  JUSTICE WAS DONE!!  YOU HAVE    SUCCESSFULLY SOLVED THIS MURDER MYSTERY BY SHOOTING "G$(TI):PRINT:PRINTG$(P)" IS THE BEST":PRINT"DETECTIVE EVER! ":GOTO900
850 IF RN>15 THEN PRINT"YOUR SHOT MISSED! IT RICOCHETED AND KILLED YOU!!!":A(P)=10:T=4:GOTO1280
860 IF RN<7 THEN PRINT"YOU INJURED "G$(X)".":PRINT"YOU HAVE BEEN SENTENCED TO DIE  FOR ATTEMPTED MURDER":T=4:T(X)=1:T$(X)="WAS INJURED BY A MANIAC KILLER":A(P)=10:GOTO 1280
870 IF RN<11 THENPRINT"YOU HAVE SHOT AT AN INNOCENT    PERSON. YOU ARE QUITE LUCKY THISTIME- YOU MISSED HIM BUT YOU    LOSE THIS AND FOUR TURNS.":T(P)=4:T$(P)="SHOULDN'T SHOOT INNOCENT PEOPLE":T=4:GOTO 1280
880 PRINT"YOU'RE LUCKY YOUR GUN MISSFIRED.YOU SHOT AT AN INNOCENT PERSON!!":GOTO 1280
890 CLS:PRINT"YOU HAVE ALL DIED! WHAT A BUNCH OF INCOMPETENT DETECTIVES!!":PRINT:PRINT"FOR YOUR INFORMATION THE KILLER WAS "G$(TI)"."
900 PRINT:INPUT" ANOTHER MYSTERY (Y/N)";X$:IF X$="Y" THEN RUN
910 PRINT"PLEASE!"Q$"?":INPUT X$:IF LEFT$(X$,1)="N"THEN Q$=Q$+"!!":GOTO 910
920 PRINT"THANK YOU, I THOUGHT YOU MIGHT.":FOR X=1TO1500:NEXT X:RUN
930 FOR P=1TO5
940 IF P>5 THEN GOTO 930
950 IF A(P)=10 THEN FOR X=1TOY:IF A(X)<>10 THEN NEXTP ELSE NEXT X:GOTO 890
960 IF G1(P)=1 THEN G(P)=G(P)+1
970 IF G1(P)=1 AND G(P)>24 THEN G(P)=1
980 IF G1(P)=1 THENNEXT P:GOTO 930
990 PLAY "V30;L15;A"
1000 IF T(P)<=0 THEN 1030
1010 IF T(P)>0 THEN CLS:PRINT@174,G$(P):PRINT@225,T$(P):PRINT@299,"LOSES TURN":PRINT@483,"PRESS <ENTER> TO CONTINUE":T(P)=T(P)-1
1020 X$=INKEY$:IF X$<>CHR$(13)THEN 1020 ELSE NEXT P:GOTO 930
1030 CLS:PRINT@174,G$(P):PRINT@228,"PRESS <ENTER> TO CONTINUE"
1040 X$=INKEY$:IF X$<>CHR$(13)THEN 1040
1050 P1=G(P):T=0
1060 T=T+1
1070 CLS:PRINT"i'm in the "L$(P1) 
1080 PRINT"i see:":PRINTO$(P1)
1090 FOR X=1TO5:IF G(X)<>P1 THEN NEXT X:GOTO 1120
1100 IF X><P THEN PRINT G$(X)" IS HERE."
1110 NEXT X
1120 FOR X=1TO27
1130 IF E(X)<>P1 THEN NEXT X:GOTO 1150
1140 PRINTE$(X):NEXT X
1150 PRINT"it looks like i can go:"
1160 FOR X=1TO8
1170 IF DN(X)=P1 THEN PRINT"NORTH,";ELSE NEXT X
1180 FOR X=1TO9
1190 IF DS(X)=P1 THEN PRINT"SOUTH,";ELSE NEXT X
1200 FOR X=1TO15
1210 IF DE(X)=P1 THEN PRINT"EAST,";ELSE NEXT X
1220 FOR X=1TO14
1230 IF DW(X)=P1 THENPRINT"WEST,";ELSE NEXT X
1240 FOR X=1TO4
1250 IF DD(X)=P1 THEN PRINT"DOWN,";ELSE NEXTX
1260 FOR X=1TO4:IF DU(X)=P1 THEN PRINT"UP,"ELSE NEXT X
1270 PRINT
1280 G(P)=P1:IF T=4 THEN FOR X=1TO2400:NEXT X:NEXTP:GOTO 930
1290 PRINT"what now "G$(P);:INPUT A$
1300 RN=RND(20)
1310 IF LEFT$(A$,4)="LOOK" THEN 1070
1320 IF A$="VOC"THEN GOSUB 2290:GOTO 1290
1330 FOR X=1TOLEN(A$)
1340 IF MID$(A$,X,1)<>" "THEN NEXT X
1350 NOUN$=MID$(A$,X+1,3)
1360 FOR N=1TO(LEN(NU$)/3)
1370 IF MID$(NU$,3*N-2,3)<>NOUN$ THEN NEXTN
1380 VERB$=LEFT$(A$,2)
1390 FOR V=1TOLEN(VR$):IF MID$(VR$,2*V-1,2)<>VERB$ THEN NEXT V
1400 ON V GOTO 40,540,360,440,650,770,810,620,700
1410 GOTO 90
1420 CLS:PRINT@74,"**WHODUNIT**":PRINT@168,"A MURDER MYSTERY":PRINT@271,"BY":PRINT@330,"DAVE VINNEDGE":PRINT@459,"AUGUST 1983":FOR X=1TO1500:NEXT X
1430 DIM L$(25),DN(8),DS(9),DE(15),DW(14),DD(4),DU(4),O$(24),E$(27),E(27),R$(27),CL$(5,5)
1440 VR$="GOEXGEDRDELOSHINCL":NU$="NORSOUEASWESDOWWHIHAI3X5ALCEMPBLAREDCHIPAGSTOSTUDOGBLOCANGARTONBROSILHAZBIRDIRBATTRONO CATANCCOR"
1450 IT=RND (5)
1460 M1$="T4O2L2BO3CC#CO2BO3CC#C"
1470 M2$="T2O3L8EL16F#F#L8F#L4F#L8EEEEL16GGL4GL8F#F#F#"
1480 M3$="O4L8D#L2DO3L8BAL1B"
1490 M4$="O3L8EL4GO4L8D#L4.DL16O3GL8A#L2.B"
1500 M5$="L4O3GL16AGL4.F#L8O2BO3D#L4EP16"
1510 M8$="O3L8BBP5BBP5BBL8.BL8BL16BL8BBP3"
1520 FOR X=1TO24:READL$(X):NEXT
1530 FOR X=1TO8:READ DN(X):NEXT
1540 FOR X=1TO9:READ DS(X):NEXT
1550 FOR X=1TO15:READDE(X):NEXT
1560 FOR X=1TO14:READDW(X):NEXT
1570 FOR X=1TO4:READ DD(X):NEXT
1580 FOR X=1TO4:READ DU(X):NEXT
1590 FOR X=1TO24:READO$(X):NEXT
1600 FOR X=1TO5
1610 C=RND(5):IF C=C1 OR C=C2 OR C=C3 OR C=C4 OR C=C5 THEN 1610
1620 IF IT=X THEN TI=C
1630 READ D$(C):READ G$(C):READ G1(C)
1640 ON X GOTO 1650,1660,1670,1680,1690
1650 C1=C:NEXT X:GOTO1720
1660 C2=C:NEXT X:GOTO1720
1670 C3=C:NEXT X:GOTO1720
1680 C4=C:NEXT X:GOTO1720
1690 C5=C:NEXT X
1700 FOR X=1TO27:READE$(X):NEXT
1710 FOR X=1TO27:READR$(X):NEXT
1720 CLS:PRINT"WELCOME LADIES AND GENTLEMEN ANDTHANK YOU FOR ACCEPTING THIS    INVITATION TO THE GREAT WHODUNITMYSTERY.":PRINT:INPUT"DO YOU NEED INSTRUCTIONS (Y/N)";Y$
1730 IF Y$="YES"OR Y$="Y" THEN GOSUB 2210:GOTO 1750
1740 IF Y$="N"ORY$="NO"THEN 1750 ELSE 1720
1750 CLS:INPUT"PLEASE INDICATE THE NUMBER IN   YOUR PARTY. (2-5)";Y
1760 IF Y>5 OR Y<2THEN 1750
1770 CLS:PRINT"THANK YOU, THAT'S FINE.  NOW    COULD YOU PLEASE SIGN OUR GUEST REGISTER:"
1780 FOR X=1TOY
1790 PRINT"GUEST #"X;:INPUT G$(X)
1800 FOR XY=0TOX-1:IF LEFT$(G$(X),3)=LEFT$(G$(XY),3) THEN PRINTG$(X)" AND "G$(XY)" SOUND":PRINT"ALIKE TO ME. USE A DIFFERENT    NAME FOR "G$(X)", GUEST #"X:XY=X:NEXTXY:GOTO1790
1810 NEXT XY
1820 T(P)=0:G1(X)=2:NEXT X
1830 CLS:PRINT"THE FOLLOWING ARE CHARACTER     DESCRIPTIONS.":PLAY"XM1$;"
1840 FOR X=1TO5
1850 CLS:PRINT@45,G$(X):PRINTD$(X)
1860 PLAY "XM2$;XM3$;XM8$;XM2$;XM3$;XM5$;P4"
1870 NEXT X
1880 CLS:PRINT@195,"AND LET THE MYSTERY BEGIN...."
1890 PLAY M4$
1900 FOR X=1TO1000:NEXT
1910 FOR X=0TO20 STEP 5:E((C1+X))=RND(24):E((C2+X))=RND(24):E((C3+X))=RND(24):NEXT X
1920 E((IT*5-5+(C4)))=RND(24)
1930 G=1:FOR X=1TO5
1940 G(X)=RND(24)
1950 NEXT X
1960 E(27)=RND(25):IF E(27)=18 THEN 1960
1970 RETURN
1980 DATA KITCHEN,HALL,LIVING ROOM,DINING ROOM,HALL,DEN,LIBRARY,HALL,GAME ROOM,SERVENTS QUARTERS,HALL,GARAGE,MUSIC ROOM,HALL,BATHROOM,WINE CELLAR,BASEMENT,SWIMMING POOL,MASTER BEDROOM,BALCONY,GUEST BEDROOM,CHIMNEY,UPSTAIRS BATH,ATTIC
1990 DATA 4,5,8,9,11,14,17,23,1,2,5,6,8,11,16,19,20,1,2,3,4,5,7,8,9,10,11,12,13,14,19,20,2,3,5,6,8,9,10,11,12,14,15,18,20,21,14,20,22,24,2,17,20,22
2000 DATAA LARGE POT OF CLAM CHOWDER     SIMMERING ON THE STOVE.  A SINK FULL OF DIRTY DISHES.,A SPIRAL STAIRCASE WITH CHROME  RAILING LEADING UP.,A BRICK FIREPLACE WITH A LARGE  MANTLE ON THE EAST WALL.
2010 DATAA BEAUTIFUL CRYSTAL CHANDELIER. A CHINA HUTCH ON THE SOUTH WALL.
2020 DATALOTS OF PLANTS UNDER A LARGE SKYLIGHT.,A LARGE OAK DESK AND COLOR      COMPUTER,LOTS OF BOOKS,A BEAUTIFUL PAINTING ON THE EASTWALL.,A COLOR COMPUTER CONNECTED TO A GIANT SCREEN T.V.,A COZY LITTLE ROOM,A COAT RACK,A LARGE CAR. LOTS OF TOOLS.
2030 DATAA GRAND PIANO IS IN THE CENTER  OF THE ROOM--THERE IS A GOLD    CANDELABRA ON IT.,A DIMLY LIT CORRIDOR WITH A     NARROW STAIRCASE WINDING DOWN.,A SUNKEN TUB,LOTS OF DUSTY BOTTLES,A DIRTY ROOM WITH LOTS OF EMPTY CRATES
2040 DATAA LARGE KIDNEY SHAPED POOL WITH SPARKLING BLUE WATER.,A KINGSIZE WATERBED AND STONE   FIREPLACE ON SOUTH WALL.,A WROUGHT IRON RAILING,A MIRRORED CEILING AND SUNKEN   JACCUZI TUB.,SOOT,SMALL BUT PRACTICAL ROOM,DUST-OLD CLOTHES-LOTS OF BOX'S
2050 DATAIS THE VICTIM'S GRANDMOTHER. SHEIS PLUMP AND JOLLY WITH WHITE   HAIR WORN IN A BUN.  SHE IS     ALWAYS SEEN WEARING A COOKING   APRON AND WHITE SILK STOCKINGS. SHE WEARS WIRE RIMMED SPECTACLESAND LOVES COLA (JUST A SPLASH OFRUM).
2060 DATAGRANDMA,1
2070 DATAIS THE MAID. SHE'S VERY TALL ANDTHIN. SHE HAS LONG BLACK HAIR   WHICH SHE WEARS IN A BRAID TIED WITH A RED RIBBON. SHE IS ALWAYSSEEN WITH A FEATHER DUSTER IN   ONE HAND AND A DIME ROMANCE     NOVEL IN THE OTHER.
2080 DATAMAID,1
2090 DATAIS THE LONG LOST DAUGHTER WHO'S SHOWN UP AT THE WILL READING.   SHE IS VERY SEXY AND  SENSUOUS. HER HAIR IS CURLY AND BLONDE-HEREYES BLUE. SHE DIETS (BUT IS    KNOWN TO HIDE CANDY BARS) SHE   WEARS THE LATEST IN DESIGNER    FASION.
2100 DATADAUGHTER,1
2110 DATAIS THE FAMILY DOCTOR. HE'S IN   HIS MID 50'S. HE IS VERY DISTIN-GUISHED LOOKING WITH HIS PIPE.  HE HAS BROWN HAIR WHICH IS      SLIGHTLY GREYING AROUND THE     TEMPLES. HE HAS A SLIGHT GERMAN ACCENT.
2120 DATADOCTOR,1
2130 DATAIS THE GARDENER. EXCEPT FOR A   FEW GREY HAIRS HE IS BALD.  HE  ALWAYS HAS TOOLS HANGING FROM   HIS WAIST AND HIS HANDS ARE     USUALLY COVERED WITH SOIL. SPEAKLOUDLY- HE IS HARD OF HEARING.
2140 DATAGARDENER,1
2150 DATAA WHITE HAIR,A HAIRPIN,A 3X5 CARD,AN ALCOHOL BOTTLE,AN EMPTY GLASS CASE,A BLACK HAIR,A RED THREAD,A CHICKEN(?) FEATHER,A PAGE TORN FROM A BOOK,A STORE RECEIPT,A STUB OF AN AIRLINE TICKET,A DOG SNIFFING THE GROUND,A BLONDE HAIR
2160 DATAA CANDY WRAPPER,A GARMENT BAG,A TONGUE DEPRESSOR,A BROWN HAIR,A SILVER MEDALLION,A HAZE IN THE AIR,A BIRTHDAY CARD,DIRT STAINS,A BATTERY,A TROWEL,NO HAIR AT ALL,A CAT TWITCHING HER NOSE,AN ANCIENT LOOKING SCRIPT,A CORPSE
2170 DATA,TO PIN A BUN,A RECIPE FOR APPLE PIE,FOR INTERNAL USE ONLY,MFG. BY THE WIRE RIM GLASS CO.,,LEADS TO A RIBBON,I CAN'T SEE THROUGH ALL THE DUST,HER HANDS BECAME WET WITH PER-  SPIRATION AS THEIR EYES MET...
2180 DATATHANK YOU FOR SHOPPING AT THE   TALL GIRLS SHOP,JAN. 21ST...THE SAME DAY AS THE WILL READING,HE SMELLS PERFUME-VERY SEXY AND SENSUOUS,,NEW AND IMPROVED,A HALSTON ORIGINAL
2190 DATALOOKS LIKE A POPSICLE STICK,,SOME KIND OF WRITTING-MAYBE     GERMAN,LOOKS AND SMELLS LIKE PIPE SMOKE,HAPPY 54TH,GO LOOK IN YOUR GARDEN IF YOU   WANT TO EXAMINE SOME DIRT,QUITE SMALL-MAYBE FROM A HEARINGAID
2200 DATABRIGHT SILVER WITH WOOD HANDLE,,SHE SMELLS PESTICIDES AND MANURE,YOU ARE WELL LIKED- YOU GET TWO EXTRA MOVES THIS TURN.,FEELS CLAMMY- SMELLS TERRIBLE
2210 CLS:PRINT" WHODUNIT IS A MURDER MYSTERY   GAME FOR 2-5 PLAYERS. THE COM-  PUTER WILL SELECT ONE OF THE    CHARACTERS AT RANDOM AS THE     MURDERER. IT WILL THEN PLACE    CLUES THROUGHOUT THE HOUSE."
2220 PRINT"IT'S THEN YOUR JOB TO ROAM ABOUTTHE HOUSE GATHERING INFORMATION.BE CAREFUL NOT TO ACCUSE SOME-"
2230 PRINT"ONE PREMATURLY BECAUSE THERE    WILL BE CLUES POINTING TO EVERY-ONE! HOWEVER THERE WILL BE FOUR CLUES POINTING TO THEGUILTY     PERSON WHILE EVERYONE ELSE WILL HAVE FEWER.":PRINT@484,"PRESS ENTER TO CONTINUE";:INPUT X$
2240 CLS:PRINT"IF YOU ARE NOT SURE WHO A CLUE  BELONGS TO YOU MAY EXAMINE IT,  BUT IT IS UNLAWFUL TO REMOVE    EVIDENCE FROM A ROOM.            ONCE YOU FEEL YOU KNOW WHO THE MURDERER IS IT IS YOUR JOB TO   FIND AND ELIMINATE HIM/HER."
2250 PRINT"IF YOU FEEL THAT YOU ARE THE    GUILTY PERSON YOU MUST THEN     FIND AND HIDE THE BODY. (THE    SWIMMING POOL IS A GOOD PLACE   TO HIDE A BODY.)                 A COMPUTER SCRATCH PAD IS PRO- VIDED FOR KEEPING TRACK OF CLUES"
2260 PRINT@484,"PRESS ENTER TO CONTINUE";:INPUT X$
2270 CLS:PRINT" THE GAME IS PLAYED BEST WHEN   ONLY THE PERSON WHOSE TURN IT   IS CAN SEE THE SCREEN, BUT THAT IS LEFT UP TO YOU TO DECIDE.
2280 PRINT@484,"PRESS ENTER TO CONTINUE";:INPUT X$
2290 CLS:PRINT" THE AVAILABLE VERBS ARE:":PRINT:PRINT"GO ------- TO MOVE":PRINT"GET ------ TO PICK UP OBJECTS":PRINT"EXAMINE -- TO LOOK AT EVIDENCE":PRINT"DROP ----- TO DROP OBJECTS":PRINT"DESCRIBE - DESCIBES A CHARACTER"
2300 PRINT"LOCATE --- TO FIND A CHARACTER":PRINT"SHOOT ---- TO DEAL WITH MURDERER";:PRINT"LOOK ----- GIVES YOUR LOCATION":PRINT"INV ------ LIST WHAT YOU HOLD":PRINT"VOC ------ LIST THESE VERBS":PRINT"CLUE ----- SCRATCH PAD"
2310 PRINT@484,"PRESS ENTER TO CONTINUE";:INPUT X$
2320 CLS:PRINT"EXCEPT FOR-",,,,"*LOOK",,"*INV",,"*VOC",,"*CLUE":PRINT:PRINT"WHICH ARE USED ALONE, USE TWO   WORD COMMANDS SUCH AS-":PRINT:PRINT"*GO NORTH*"
2330 PRINT@483,"PRESS ENTER TO CONTINUE";:INPUT X$
2340 CLS:RETURN
