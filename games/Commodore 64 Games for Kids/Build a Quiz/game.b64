1 REM BUILD A QUIZ
100 DI=8:TA=1:REM ** DEVICE NUMBERS **
110 VC=53248:POKEVC+32,0:POKEVC+33,11:PRINTCHR$(147)
120 Z$=CHR$(13):DP=0:PS=0
130 DIM NT%(30)
140 MC=54272:FORX=MCTO54296:POKEX,0:NEXTX
150 FORX=0TO30:READZ:NT%(X)=Z:NEXTX
160 M=((PEEK(53272)AND240)/16)*1024
170 C=55296
200 PRINT"{WHT}{2 DOWN}{RVS}{6 SPACE}B U I L D{4 SPACE}A{4 SPACE}Q U I Z{7 SPACE}{OFF}"
210 PRINT"{2 DOWN} OPTION 1 OF THIS PROGRAM WILL BUILD A"
215 PRINT" QUIZ AND SAVE IT ON TAPE OR DISK."
220 PRINT"{2 DOWN} OPTION 2 ALLOWS YOU RECALL AN EXISTING"
225 PRINT" QUIZ AND TEST YOURSELF."
227 GOSUB9000
230 PRINT"{2 DOWN}{3 SPACE}ENTER OPTION:"
240 PRINT"{DOWN}{3 SPACE}{RVS}1{OFF} CREATE A QUIZ":PRINT"{DOWN}{3 SPACE}{RVS}2{OFF} TAKE A QUIZ"
245 PRINT"{DOWN}{3 SPACE}{RVS}3{OFF} END PROGRAM"
250 GETX$:IFX$=""THEN250
260 X=VAL(X$):ONXGOTO300,700,280
270 GOTO250
280 PS=0:GOSUB9000:PRINTCHR$(147)
290 END
300 NQ=0:GOSUB9200:GOSUB7500:PRINTCHR$(147)
310 OPEN9,DV,1,QN$:PRINTCHR$(147):PRINT#9,X$;Z$;
320 NQ=NQ+1:PRINT"{CLR}{2 DOWN}{3 SPACE}ENTER QUESTION TYPE:{2 DOWN}"
330 PRINT"{2 DOWN}{3 SPACE}{RVS}1{OFF} TRUE/FALSE":PRINT"{2 DOWN}{3 SPACE}{RVS}2{OFF} MULTIPLE CHOICE"
335 PRINT"{2 DOWN}{3 SPACE}{RVS}3{OFF} COMPLETION"
340 PRINT"{2 DOWN}{3 SPACE}{RVS}4{OFF} (ALL DONE)"
350 GETX$:IFX$=""THEN350
360 IFX$="4"THENT$=X$:GOTO550
365 IFX$<"1"ORX$>"3"THEN350
370 PRINT"{CLR}{DOWN}{3 SPACE}QUESTION #";NQ;
380 X=VAL(X$):T$=X$:ONXGOTO400,450,500
390 GOTO350
400 PRINT"(TRUE/FALSE)":O=240:L=80:GOSUB8000:A$=X$
410 PRINT"{10 DOWN}{3 SPACE}ENTER CORRECT ANSWER ({RVS}T{OFF} OR {RVS}F{OFF})"
420 GETX$:IFX$=""THEN420
430 IFX$<>"T"ANDX$<>"F"THEN420
435 PRINTCHR$(147)
440 PRINT#9,T$;Z$;A$;Z$;X$;Z$;:GOTO320
450 PRINT"(MULTIPLE CHOICE)":O=240:L=80:GOSUB8000
455 Q$=X$:PRINT"{8 DOWN} A.":O=404:L=76:GOSUB8000
460 A$=X$:PRINT"{2 DOWN} B.":O=524:GOSUB8000:B$=X$
465 PRINT"{2 DOWN} C.":O=644:GOSUB8000:C$=X$
470 PRINT"{2 DOWN} D.":O=764:GOSUB8000:D$=X$
480 PRINT"{3 DOWN}{4 SPACE}ENTER CORRECT ANSWER ({RVS}A{OFF},{RVS}B{OFF},{RVS}C{OFF} OR {RVS}D{OFF})"
485 GETX$:IFX$=""THEN485
490 IFX$<"A"ORX$>"D"THEN485
495 PRINTCHR$(147)
497 PRINT#9,T$;Z$;Q$;Z$;A$;Z$;B$;Z$;C$;Z$;D$;Z$;X$;Z$;
499 GOTO320
500 PRINT"(COMPLETION)":O=240:L=80:GOSUB8000:Q$=X$
505 PRINT"{10 DOWN}{3 SPACE}ENTER CORRECT ANSWER:"
510 O=600:L=80:GOSUB8000
520 PRINTCHR$(147)
530 PRINT#9,T$;Z$;Q$;Z$;X$;Z$;
540 GOTO320
550 PRINTCHR$(147)
560 PRINT#9,T$;Z$;
570 CLOSE9
580 GOTO200
700 GOSUB9200:GOSUB7500:PRINTCHR$(147)
730 OPEN9,DV,0,QN$:GOSUB6000
740 IFLEN(X$)>39THEN760
750 A$=" "+X$:X$=A$+" ":GOTO740
760 PRINT"{CLR}{2 DOWN}":FORX=1TO11:PRINTX$;:PRINT" ":NEXTX:GOSUB7000:FORX=1TO1500:NEXTX
770 PRINTCHR$(147):NQ=0:CQ=0
800 INPUT#9,X$
810 IFX$="4"THEN2000
820 IFX$<"1"ORX$>"3"THEN800
830 NQ=NQ+1:X=VAL(X$)
850 ONXGOTO900,1000,1100
860 GOTO800
900 GOSUB6000:Q$=X$:GOSUB6000:R$=X$
902 PRINT"{CLR}{DOWN}{3 SPACE}QUESTION #";NQ
904 PRINT"{3 DOWN}";Q$
910 PRINT"{4 DOWN}{3 SPACE}ENTER {RVS}T{OFF} FOR TRUE"
920 PRINT"{2 DOWN}{3 SPACE}ENTER {RVS}F{OFF} FOR FALSE"
940 GETA$:IFA$=""THEN940
950 IFA$<>"T"ANDA$<>"F"THEN940
960 IFA$<>R$THEN1200
970 GOTO1300
1000 GOSUB6000:Q$=X$:GOSUB6000:A$=X$:GOSUB6000:B$=X$:GOSUB6000:C$=X$
1002 GOSUB6000:D$=X$:GOSUB6000:R$=X$
1006 PRINT"{CLR}{DOWN}{3 SPACE}QUESTION #";NQ
1008 PRINT"{3 DOWN}";Q$:PRINT"{2 DOWN} A. ";A$:X$=A$:GOSUB6700
1010 PRINT" B. ";B$:X$=B$:GOSUB6700:PRINT" C. ";C$:X$=C$:GOSUB6700
1020 PRINT" D. ";D$:X$=D$:GOSUB6700
1030 PRINT"{12 SPACE}ENTER {RVS}A{OFF},{RVS}B{OFF},{RVS}C{OFF} OR {RVS}D{OFF}"
1040 GETA$:IFA$=""THEN1040
1050 IFA$<"A"ORA$>"D"THEN1040
1060 IFA$<>R$THEN1200
1070 GOTO1300
1100 GOSUB6000:Q$=X$:GOSUB6000:R$=X$
1102 PRINT"{CLR}{DOWN}{3 SPACE}QUESTION #";NQ
1110 PRINT"{3 DOWN}";Q$
1120 O=480:L=80:GOSUB8000
1130 IFX$<>R$THEN1200
1140 GOTO1300
1200 GOSUB7200:PRINT"{CLR}{3 DOWN}CORRECT ANSWER:{3 DOWN}":PRINTR$
1210 FORX=1TO1500:NEXTX:PRINTCHR$(147):GOTO800
1300 CQ=CQ+1:GOSUB7000
1310 PRINTCHR$(147)
1320 GOTO800
2000 PRINT"{CLR}{2 DOWN}{15 SPACE}{RVS}QUIZ OVER!{OFF}"
2010 PRINT"{2 DOWN}{3 SPACE}NUMBER OF QUESTIONS =";NQ
2020 PRINT"{2 DOWN}{3 SPACE}NUMBER CORRECT{6 SPACE}=";CQ
2030 IFNQ=0THENNQ=1
2040 X=INT((CQ*100)/NQ)
2050 PRINT"{2 DOWN}{3 SPACE}YOUR SCORE{10 SPACE}=";X;"{LEFT}%"
2060 PRINT"{3 DOWN}{12 SPACE}(PRESS ANY KEY)"
2070 GETX$:IFX$=""THEN2070
2080 PRINTCHR$(147)
2090 CLOSE 9
2100 GOTO200
6000 X$=""
6010 GET#9,R$
6020 IFR$=""THEN6010
6030 IFASC(R$)=13THEN6060
6040 X$=X$+R$
6050 GOTO6010
6060 RETURN
6700 X=LEN(X$):IFX=36ORX=76THEN6720
6710 PRINT"{SSPACE}"
6720 RETURN
7000 POKEMC+0,0:POKEMC+1,0:POKEMC+5,15:POKEMC+6,15:POKEMC+24,10
7010 FORX=2TO11
7020 POKEVC+33,X:POKEMC+1,X*5:POKEMC+4,33
7030 FORY=1TO50:NEXTY
7040 POKEMC+4,32:FORY=1TO10:NEXTY,X
7050 POKEMC+24,0
7060 RETURN
7200 POKEMC+0,0:POKEMC+1,30:POKEMC+4,33:POKEMC+5,15:POKEMC+6,15
7210 POKEVC+33,0:POKEMC+24,10
7220 FORX=1TO500:NEXTX
7230 POKEMC+4,32:FORX=1TO20:NEXTX
7240 POKEMC+24,0:POKEVC+33,11
7250 RETURN
7500 PRINT"{CLR}{3 DOWN}{3 SPACE}ENTER QUIZ NAME:":PRINT"{DOWN}{3 SPACE}(1-37 LETTERS)"
7510 O=323:L=38:GOSUB8000:IFLEN(X$)>37THEN7500
7520 QN$=X$:IFLEN(X$)>11THENQN$=LEFT$(X$,11)
7530 QN$="QUIZ/"+QN$
7540 PRINT"{6 DOWN}{3 SPACE}PREPARE ";DV$:PRINT"{DOWN}{3 SPACE}THEN PRESS {RVS}RETURN{OFF}"
7550 GETE$:IFE$=""THEN7550
7560 IFASC(E$)<>13THEN7550
7570 RETURN
8000 X$="":POKEM+O,160:POKEC+O,1
8010 GETY$:IFY$=""THEN8010
8020 X=ASC(Y$):IFX=13THEN8150
8030 IFX=20THEN8100
8040 Y=LEN(X$):X$=X$+Y$:IFX>63THENX=X-64
8050 POKEM+O+Y,X:POKEC+O+Y,1:POKEM+O+Y+1,160:POKEC+O+Y+1,1:IFLEN(X$)<LTHEN8010
8060 GOTO8150
8100 Y=LEN(X$):IFY<1THEN8010
8110 POKEM+O+Y,32:POKEC+O+Y,11:POKEM+O+Y-1,160:POKEC+O+Y-1,1
8120 Y$=LEFT$(X$,Y-1):X$=Y$:GOTO8010
8150 Y=LEN(X$):POKEM+O+Y,32:POKEC+O+Y,11
8160 RETURN
9000 IFPS=1THEN9080
9005 POKEMC+0,0:POKEMC+1,0:POKEMC+5,79:POKEMC+6,129:POKEMC+24,15
9010 FORX=0TO30
9020 Y=INT(NT%(X)/256)
9030 POKEMC+0,NT%(X)-(Y*256)
9040 POKEMC+1,Y:POKEMC+4,17
9050 FORY=1TO70:NEXTY
9060 POKEMC+4,16:FORY=1TO10:NEXTY,X
9070 POKEMC+24,0:PS=1
9080 RETURN
9200 IFDP=1THEN9280
9205 PRINTCHR$(147);"{4 DOWN} DO YOU WANT TO USE DISK OR TAPE FOR"
9210 PRINT"{DOWN} SAVING/LOADING QUIZZES?"
9220 PRINT"{5 DOWN} ENTER {RVS}D{OFF} OR {RVS}T{OFF}"
9230 GETX$:IFX$=""THEN9230
9240 IFX$="D"THENDV=DI:DV$="DISK":GOTO9270
9250 IFX$="T"THENDV=TA:DV$="TAPE":GOTO9270
9260 GOTO9230
9270 DP=1
9280 RETURN
9900 DATA6430,6430,6430,4817,8101,8101,8101,6430,6430,8101,9634,9634
9910 DATA8583,8101,7217,0,7217,8101,8583,8583,8101,7217
9920 DATA8101,6430,6430,8101,7217,4817,6069,7217,6430
