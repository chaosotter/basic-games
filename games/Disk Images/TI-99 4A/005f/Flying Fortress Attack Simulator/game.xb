1 REM  FLYING FORTRESS
2 REM
3 REM  ATTACK SIMULATOR
4 REM
5 REM
6 REM  BY HOWARD UMAN
7 REM
8 REM
9 REM
10 REM  REQUIRES:
11 REM
12 REM         Extended BASIC              32K Expansion               Disk System                 Joystick #1
13 REM
14 REM
15 REM  Thanks 2 MICROpendium       for the "SCRLDN"            routine.
16 REM
17 REM  And to Miller's             Graphics for some of        the routines and            graphic characters.
18 REM
19 REM
20 DATA 0101030206742C3E3F7FFF1F0301030700008080C05C68F8F8FCFEF0800080C0
21 GOTO 27 :: A$
22 A,ABC,AI,ALT,AV,AZ,BOMBS,BONUS,C,DE,DIS,DISTANCE,E,ECR,EJR,ERUN,FUEL,H,HISCORE,HITS,I,II,K,KE,KO,L,LEV,LEVEL,LO,LX,LY,PITCH,S,SCORE,SI
23 SIL,SILOS,SO,SPEED,ST,T,X,X1,XL,XLO,XPLANE,XX,XXX,Y,Y1,YL,YLO,YPLANE,YY,YYY,Z,ZZ
24 SHIT
25 CALL CHAR :: CALL CLEAR :: CALL COINC :: CALL COLOR :: CALL DELSPRITE :: CALL HCHAR :: CALL INIT :: CALL JOYST :: CALL KEY :: CALL LINK :: CALL LOAD :: CALL LOCATE :: CALL MAGNIFY :: CALL MOTION
26 CALL PATTERN :: CALL POSITION :: CALL SCREEN :: CALL SOUND :: CALL SPRITE :: CALL VCHAR :: !@P-
27 CALL INIT :: CALL LOAD(8196,63,248) :: CALL LOAD(16376,83,67,82,76,68,78,48,0)
28 CALL LOAD(12288,2,224,131,224,4,192,2,1,37,20,2,2,2,224,4,32,32,44)
29 CALL LOAD(12306,2,1,36,244,2,2,3,0,4,32,32,36,4,91)
30 FOR C=9460 TO 9492 :: CALL LOAD(C,128) :: NEXT C
31 REM  THANKS MICROpendium         FOR THE SCROLL DOWN         ROUTINE!!!
32 CALL CLEAR
33 CALL SCREEN(6) :: FOR I=1 TO 8 :: CALL COLOR(I,16,1) :: NEXT I
34 DISPLAY AT(8,5):"********************" :: DISPLAY AT(9,5):"*                  *" :: DISPLAY AT(10,5):"* FLYING  FORTRESS *"
35 DISPLAY AT(11,5):"*                  *" :: DISPLAY AT(12,5):"* ATTACK SIMULATOR *" :: DISPLAY AT(13,5):"*                  *"
36 DISPLAY AT(14,5):"********************"
37 DISPLAY AT(16,6):"FAIRWARE PROGRAM" :: DISPLAY AT(19,3)BEEP:"(C) 1986 HOWARD UMAN" :: DISPLAY AT(20,12):"3913 SYBIL ROAD"
38 DISPLAY AT(21,12):"RAND., MD  21133"
39 RANDOMIZE :: CALL MAGNIFY(4) :: DISPLAY AT(24,1)BEEP:"INITIALIZING.....PLEASE WAIT"
40 ABC=1 :: HISCORE=0
41 !
42 DATA 03070F1F3F7FE2EAEAE27F3F1F0F0703C0E0F0F8FCFEBFBFBF8FFEFCF8F0E0C0
43 DATA 00000000AA02071F2747878FFF3EA27F00000000AA00008687FFFFFF000000C0
44 DATA 000111238B4311010000000000000000000080FCFFFC80000000000000000000
45 DATA 0000013FFF3F01000000000000000000008088C4D1C288800000000000000000
46 DATA 0000000000100208000000040100000000000000800020008000200000000000
47 DATA 0000080000004200000400100000008000000240000000080000004000000800
48 DATA 0101030307070F0F1F1F3F3F7F7FFFFF8080C0C0E0E0F0F0F8F8FCFCFEFEFFFF
49 DATA 0305081224322A140904020100000001C020903048944C24884830E0808080C0
50 DATA 08081C1C1C1C1C3E7F1C00082210040800000000000000000000000000000000
51 DATA 0000000003040A09090A04030000000000000000C0205090905020C000000000
52 DATA 0406070F0F8FDFFFDF8F0F0F070604000020E0A0C0F09C079CF0C0A0E0200000
53 DATA 00000000000000AA550000000000000000000000000000A04000000000000000
54 DATA 0000000001023F010000000000000000000080804020FEC00000000000000000
55 FOR I=92 TO 140 STEP 4 :: READ A$ :: CALL CHAR(I,A$)
56 NEXT I
57 READ A$ :: CALL CHAR(36,A$)
58 SCORE,HITS=0 :: DISPLAY AT(12,3)ERASE ALL BEEP:"STARTING LEVEL?  (1-6)"
59 CALL KEY(3,K,S) :: IF S=0 THEN 59 ELSE IF K<49 OR K>54 THEN CALL SOUND(-1,220,0) :: GOTO 59 ELSE LEVEL=K-48
60 CALL MAGNIFY(4) :: FUEL=(3+LEVEL*1.5)*1000+750 :: CALL CLEAR :: CALL COLOR(11,15,15,12,15,1) :: FOR I=1 TO 8 :: CALL COLOR(I,2,1) :: NEXT I :: CALL SCREEN(8)
61 ERUN=350 :: DISPLAY AT(3,14):"xz" :: DISPLAY AT(4,14):"y{"
62 DISPLAY AT(5,13):"xwwz"
63 DISPLAY AT(6,13):"yww{"
64 DISPLAY AT(7,12):"xwwwwz"
65 DISPLAY AT(8,12):"ywwww{"
66 DISPLAY AT(9,11):"xwwwwwwz"
67 DISPLAY AT(10,11):"ywwwwww{"
68 DISPLAY AT(11,10):"xwwwwwwwwz"
69 DISPLAY AT(12,10):"ywwwwwwww{"
70 DISPLAY AT(13,9):"xwwwwwwwwwwz"
71 DISPLAY AT(14,9):"ywwwwwwwwww{"
72 DISPLAY AT(15,8):"xwwwwwwwwwwwwz"
73 DISPLAY AT(16,8):"ywwwwwwwwwwww{"
74 DISPLAY AT(17,7):"xwwwwwwwwwwwwwwz"
75 DISPLAY AT(18,7):"ywwwwwwwwwwwwww{"
76 DISPLAY AT(19,6):"xwwwwwwwwwwwwwwwwz"
77 DISPLAY AT(20,6):"ywwwwwwwwwwwwwwww{"
78 DISPLAY AT(21,5):"xwwwwwwwwwwwwwwwwwwz"
79 DISPLAY AT(22,5):"ywwwwwwwwwwwwwwwwww{"
80 DISPLAY AT(23,4):"xwwwwwwwwwwwwwwwwwwwwz"
81 SPEED=0 :: DISPLAY AT(24,4):"ywwwwwwwwwwwwwwwwwwww{"
82 CALL SPRITE(#1,92,5,138,113,#2,124,3,8,9,#3,124,13,137,213,#4,92,2,140,113)
83 DISPLAY AT(1,1)BEEP:" PRESS FIRE BUTTON TO START"
84 CALL KEY(1,K,S) :: IF K<>18 THEN 84
85 DISPLAY AT(1,1):""
86 DISPLAY AT(1,1):"END OF" :: DISPLAY AT(2,1):"RUNWAY:" :: DISPLAY AT(3,1):ERUN; :: DISPLAY AT(1,20):"SPEED:" :: DISPLAY AT(2,20):SPEED
87 FOR I=1 TO 50 :: SPEED=SPEED+I :: ERUN=ERUN-I :: CALL SOUND(-4000,111,30,111,30,I*75+690,30,-8,0) :: IF ERUN<0 THEN ERUN=0
88 CALL MOTION(#2,I*2,0,#3,I*2,0) :: CALL COLOR(11,2,15) :: DISPLAY AT(3,1):ERUN; :: DISPLAY AT(2,20):SPEED :: CALL JOYST(1,X,Y) :: IF Y<>4 THEN 94
89 CALL COLOR(11,15,15) :: CALL MOTION(#2,0,0,#3,0,0,#1,0,0,#4,0,0) :: CALL SOUND(-600,-6,0) :: CALL COLOR(#1,7) :: FOR DE=1 TO 60 :: NEXT DE
90 CALL SOUND(-600,-6,1) :: CALL PATTERN(#1,112,#4,112) :: FOR DE=1 TO 50 :: NEXT DE
91 CALL SOUND(-600,-6,2) :: CALL PATTERN(#1,116,#4,116) :: FOR DE=1 TO 25 :: NEXT DE :: CALL SOUND(-600,-6,3) :: FOR DE=1 TO 25 :: NEXT DE
92 FOR DE=4 TO 30 :: CALL SOUND(-400,-6,DE) :: NEXT DE :: CALL DELSPRITE(#1,#4) :: FOR DE=1 TO 100 :: NEXT DE
93 GOTO 299
94 CALL POSITION(#1,X1,Y1) :: CALL MOTION(#1,0,X,#4,0,X) :: IF Y=-4 AND SPEED>250 THEN 99 ELSE IF Y=-4 THEN 89
95 IF Y1<76 OR Y1>156 THEN 89 ELSE CALL COLOR(11,15,15) :: IF ERUN=0 THEN 97
96 NEXT I
97 CALL MOTION(#1,0,0,#4,0,0) :: DISPLAY AT(3,14):"  "
98 FOR I=4 TO 16 :: CALL SOUND(-500,I*110,0) :: DISPLAY AT(I,1):"" :: NEXT I :: GOTO 89
99 CALL COLOR(11,15,15)
100 CALL SOUND(-4000,111,30,111,30,I*75+690,30,-8,0) :: CALL MOTION(#1,-4,0,#4,-1,0) :: CALL POSITION(#1,X,Y) :: IF X>90 THEN 100
101 CALL JOYST(1,X,Y) :: CALL MOTION(#1,-6,X*.75,#4,-6,X*.75) :: CALL SOUND(-4000,111,30,111,30,I*75+1000,30,-8,0) :: CALL POSITION(#4,X,Y)
102 IF X>12 THEN 101 ELSE CALL CLEAR :: CALL DELSPRITE(ALL) :: CALL SOUND(-1,44000,30)
103 FUEL=FUEL-I*10*LEVEL :: SCORE=SCORE+((SPEED*5)-150)
104 DISPLAY AT(6,4):"FUEL REMAINING: "&STR$(FUEL) :: DISPLAY AT(5,(28-LEN("YOUR SCORE: "&STR$(SCORE)))/2):"YOUR SCORE: "&STR$(SCORE)
105 FOR I=1 TO 7 :: DISPLAY AT(12,2)BEEP:"WARNING: BANDITS 12 OCLOCK" :: FOR DE=1 TO 30 :: NEXT DE :: DISPLAY AT(12,1):""
106 FOR DE=1 TO 30 :: NEXT DE :: NEXT I
107 EJR=1+LEVEL :: CALL CLEAR :: CALL COLOR(11,2,2,14,7,7) :: IF EJR>6 THEN EJR=6
108 DISPLAY AT(1,1):"       wwwwwwwwwwwwww"
109 DISPLAY AT(2,1):"     ww  w        w  ww"
110 DISPLAY AT(3,1):"   ww    w        w    ww"
111 DISPLAY AT(4,1):"  w     w          w     w"
112 DISPLAY AT(5,1):" w      w          w      w"
113 DISPLAY AT(6,1):" w     w            w     w"
114 DISPLAY AT(7,1):"w      w            w      w"
115 DISPLAY AT(8,1):"w     w              w     w"
116 DISPLAY AT(9,1):"w     w              w     w"
117 DISPLAY AT(10,1):"w    w                w    w"
118 DISPLAY AT(11,1):"w    w                w    w"
119 DISPLAY AT(12,1):"w   w                  w   w"
120 DISPLAY AT(13,1):"w   w                  w   w"
121 DISPLAY AT(14,1):"w  w                    w  w"
122 DISPLAY AT(15,1):"w  w                    w  w"
123 DISPLAY AT(16,1):"w w                      w w"
124 DISPLAY AT(17,1):"w w                      w w"
125 DISPLAY AT(18,1):"ww                        ww"
126 DISPLAY AT(19,1):"wwwwwwwwwwwwwwwwwwwwwwwwwwww"
127 DISPLAY AT(20,1):"w                          w"
128 DISPLAY AT(21,1):"w                          w"
129 DISPLAY AT(22,1):"w                          w"
130 DISPLAY AT(23,1):"w                          w"
131 DISPLAY AT(24,1):"wwwwwwwwwwwwwwwwwwwwwwwwwwww"
132 DISPLAY AT(20,3):"FUEL REMAINING:";FUEL; :: DISPLAY AT(21,3):"HITS TAKEN:";HITS; :: DISPLAY AT(22,3):"ENEMY JETS REMAINING:";EJR;
133 DISPLAY AT(23,3):"SCORE:";SCORE;
134 CALL SPRITE(#1,132,2,65,108)
135 REM  BANDITS
136 DIS,DISTANCE=55-LEVEL*5
137 CALL SPRITE(#2,36,7,INT(RND*130)+1,INT(RND*256)+1)
138 REM  MAINFLOW
139 FOR I=1 TO DISTANCE :: CALL POSITION(#2,X,Y) :: IF X>130 THEN CALL LOCATE(#2,15,Y)ELSE IF X<10 THEN CALL LOCATE(#2,124,Y)
140 CALL JOYST(1,X,Y) :: XPLANE=INT(RND*19-9) :: YPLANE=INT(RND*19-9) :: CALL MOTION(#2,-Y*2-XPLANE,-X*2-YPLANE)
141 FUEL=FUEL-((LEVEL+1)*5+5) :: CALL KEY(1,K,S) :: IF K=18 THEN FUEL=FUEL-25 :: GOTO 148
142 CALL SOUND(-700,111,30,111,30,I*10+350,30,-8,5) :: DISPLAY AT(20,3):"FUEL REMAINING:";FUEL; :: IF FUEL<1 THEN 145
143 DIS=DIS-1 :: NEXT I :: CALL DELSPRITE(#2) :: CALL SCREEN(7) :: FOR SO=0 TO 30 :: CALL SOUND(-600,-6,SO) :: NEXT SO :: HITS=HITS+1 :: EJR=EJR-1
144 CALL SCREEN(8) :: IF HITS<5 THEN 147 ELSE CALL DELSPRITE(ALL)
145 FOR I=1 TO 7 :: DISPLAY AT(10,13)SIZE(5)BEEP:"EJECT" :: CALL SCREEN(I+2) :: FOR DE=1 TO 25 :: NEXT DE :: DISPLAY AT(10,13)SIZE(5):"" :: FOR DE=1 TO 30 :: NEXT DE :: NEXT I
146 FOR I=0 TO 24 :: CALL SCREEN(7) :: CALL SOUND(-250,-2,I) :: CALL SCREEN(8) :: CALL LINK("SCRLDN") :: NEXT I :: CALL SOUND(-1,44000,30) :: GOTO 299
147 IF EJR=0 THEN 155 ELSE DISPLAY AT(21,3):"HITS TAKEN:";HITS; :: DISPLAY AT(22,3):"ENEMY JETS REMAINING:";EJR; :: GOTO 135
148 CALL SOUND(-3000,110,30,220,30,330,30,-4,0) :: FOR Z=17 TO 11 STEP-2 :: CALL VCHAR(Z,16,136,2) :: NEXT Z
149 CALL SOUND(-3000,110,30,220,30,330,30,-4,5) :: FOR Z=17 TO 11 STEP-2 :: CALL VCHAR(Z,16,32,2) :: NEXT Z :: CALL COINC(ALL,C) :: IF C=0 THEN 142
150 CALL SCREEN(7) :: CALL COLOR(#2,2) :: CALL SOUND(-600,-6,0) :: CALL PATTERN(#2,112) :: FOR DE=1 TO 15 :: NEXT DE :: CALL SOUND(-660,-6,2)
151 CALL PATTERN(#2,116) :: FOR DE=1 TO 15 :: NEXT DE :: CALL SOUND(-100,-6,5) :: CALL DELSPRITE(#2)
152 EJR=EJR-1 :: DISPLAY AT(22,3):"ENEMY JETS REMAINING:";EJR; :: SCORE=SCORE+DIS*5 :: DISPLAY AT(23,3):"SCORE:";SCORE;
153 CALL SCREEN(8) :: IF EJR=0 THEN 155
154 GOTO 136
155 REM  BOMBING
156 CALL CLEAR :: CALL DELSPRITE(ALL) :: CALL MAGNIFY(3)
157 DISPLAY AT(6,4):"FUEL REMAINING: "&STR$(FUEL) :: DISPLAY AT(5,(28-LEN("YOUR SCORE: "&STR$(SCORE)))/2):"YOUR SCORE: "&STR$(SCORE)
158 FOR I=1 TO 7 :: DISPLAY AT(12,2)BEEP:"APPROACHING BOMBING SITE" :: FOR DE=1 TO 25 :: NEXT DE :: DISPLAY AT(12,1):""
159 FOR DE=1 TO 25 :: NEXT DE :: NEXT I :: ALT=50 :: SI=0 :: SILOS,SIL=LEVEL+1 :: IF SILOS>5 THEN SILOS,SIL=5
160 BOMBS=SILOS+2 :: CALL CLEAR :: CALL COLOR(11,13,13,12,2,2) :: FOR H=1 TO 12 :: CALL VCHAR(4,H,112,21) :: CALL VCHAR(4,33-H,112,21) :: NEXT H
161 FOR I=4 TO 24 :: CALL HCHAR(I,13,120,8) :: NEXT I
162 DISPLAY AT(1,1):"SCORE:";SCORE; :: DISPLAY AT(2,1):"HITS:";HITS; :: DISPLAY AT(1,18):"FUEL:";FUEL; :: DISPLAY AT(2,18):"BOMBS:";BOMBS;
163 DISPLAY AT(3,1):"ALTITUDE:";ALT;"M "; :: DISPLAY AT(3,18):"TARGETS:";SILOS; :: CALL SPRITE(#3,104,14,47,256,#4,108,7,84,256,#1,92,5,165,120)
164 CALL SPRITE(#5,104,14,126,1,#6,108,7,169,256,#7,104,14,211,1,#8,108,7,253,256)
165 Z=4+LEV*.5 :: CALL MOTION(#7,Z,14,#8,Z,-15,#5,Z,16,#6,Z,-17,#3,Z,18,#4,Z,-19)
166 FOR I=1 TO 35+(LEVEL*3) :: CALL JOYST(1,X,Y)
167 ALT=ALT-(Y/2) :: CALL COINC(ALL,A) :: IF A THEN 174
168 IF ALT>100 THEN ALT=100
169 DISPLAY AT(3,1):"ALTITUDE:";ALT;"M ";
170 CALL MOTION(#1,0,X*1.75) :: CALL SOUND(-999,111,30,111,30,(ALT+270)*10,30,-8,3) :: CALL COINC(ALL,A) :: IF A THEN 174
171 SCORE=SCORE+5 :: DISPLAY AT(1,1):"SCORE:";SCORE; :: FUEL=FUEL-5*(HITS*2+LEVEL) :: DISPLAY AT(1,18):"FUEL:";FUEL; :: CALL COINC(ALL,A) :: IF A THEN 174
172 IF ALT<2 OR FUEL<1 THEN 176 ELSE CALL POSITION(#1,X,Y) :: IF Y>230 THEN CALL LOCATE(#1,X,26)ELSE IF Y<26 THEN CALL LOCATE(#1,X,230)
173 NEXT I :: GOTO 180
174 HITS=HITS+1 :: DISPLAY AT(2,1):"HITS:";HITS; :: CALL COLOR(#1,7) :: CALL SOUND(-100,-2,15,110,0) :: FOR E=1 TO 25 :: NEXT E :: CALL COLOR(#1,5) :: IF HITS<5 THEN 172
175 CALL SOUND(-600,-6,0) :: CALL COLOR(#1,7) :: FOR DE=1 TO 60 :: NEXT DE
176 CALL DELSPRITE(#2,#3,#4,#5,#6,#7,#8) :: CALL MOTION(#1,0,0) :: CALL SOUND(-600,-6,1) :: CALL PATTERN(#1,112) :: FOR DE=1 TO 50 :: NEXT DE
177 CALL SOUND(-600,-6,2) :: CALL PATTERN(#1,116) :: FOR DE=1 TO 25 :: NEXT DE :: CALL SOUND(-600,-6,3) :: FOR DE=1 TO 25 :: NEXT DE
178 FOR DE=4 TO 30 :: CALL SOUND(-400,-6,DE) :: NEXT DE :: CALL DELSPRITE(#1) :: FOR DE=1 TO 100 :: NEXT DE
179 GOTO 299
180 CALL MOTION(#1,0,0) :: CALL DELSPRITE(#3,#4,#5,#6,#7,#8) :: FOR I=110 TO 960 STEP 50 :: CALL SOUND(50,I,0) :: NEXT I
181 CALL POSITION(#1,X,Y) :: IF Y>120 THEN ST=-1 ELSE ST=1
182 CALL SOUND(-4000,111,30,111,30,(ALT+270)*10,30,-8,3) :: FOR I=Y TO 120 STEP ST :: CALL LOCATE(#1,X,I) :: NEXT I
183 CALL POSITION(#1,K,L) :: CALL SOUND(-4000,111,30,111,30,(ALT+270)*10,30,-8,1) :: FOR I=K TO K-33+LEVEL STEP-1 :: CALL LOCATE(#1,I,L) :: NEXT I
184 REM  BOMBING RUN
185 FOR I=1 TO SILOS :: CALL SPRITE(#2,128,11,190,123,-6,0,#3,96,14,1,120,4,0)
186 CALL JOYST(1,X,Y) :: ALT=ALT-(Y/2) :: IF ALT>100 THEN ALT=100
187 DISPLAY AT(3,1):"ALTITUDE:";ALT;"M ";
188 IF ALT<21 THEN CALL COINC(#1,#3,16,A) :: IF A THEN 175
189 CALL MOTION(#1,0,X,#2,-4,X) :: CALL SOUND(-999,111,30,111,30,(ALT+270)*10,30,-8,3)
190 FUEL=FUEL-5*(HITS*2+LEVEL) :: DISPLAY AT(1,18):"FUEL:";FUEL; :: IF ALT>20 THEN CALL COINC(#1,#2,16,A) :: IF A THEN 201
191 IF ALT<2 THEN 176 ELSE CALL POSITION(#1,LX,LY) :: IF LY>230 THEN CALL LOCATE(#1,LX,26,#2,LX,26)ELSE IF LY<26 THEN CALL LOCATE(#1,LX,230,#2,LX,230)
192 IF FUEL<1 THEN 176 ELSE CALL POSITION(#3,XLO,YLO) :: IF XLO>175 THEN 200
193 CALL KEY(1,KE,ST) :: IF KE<>18 THEN 186 ELSE BOMBS=BOMBS-1 :: DISPLAY AT(2,18):"BOMBS:";BOMBS;
194 CALL POSITION(#1,KO,LO)
195 CALL SPRITE(#4,132,16,KO+10-ALT+INT(RND*13)-6,LO) :: IF ALT<21 THEN 204
196 CALL COINC(#3,#4,7,A) :: IF A=0 THEN CALL DELSPRITE(#4) :: IF BOMBS<>0 THEN 186 ELSE 208
197 CALL DELSPRITE(#4) :: SI=SI+1 :: SIL=SIL-1 :: CALL COLOR(#3,7) :: CALL PATTERN(#3,112) :: CALL SOUND(-100,-6,0) :: FOR DE=1 TO 34 :: NEXT DE
198 SCORE=SCORE+ALT*10 :: CALL SOUND(-100,-6,5) :: CALL PATTERN(#4,116) :: FOR DE=1 TO 34 :: NEXT DE :: IF BOMBS=0 THEN 208
199 DISPLAY AT(1,1):"SCORE:";SCORE; :: DISPLAY AT(3,18):"TARGETS:";SIL; :: IF SIL=0 THEN 208
200 NEXT I :: GOTO 208
201 CALL POSITION(#2,X,Y) :: CALL LOCATE(#2,210,Y)
202 HITS=HITS+1 :: DISPLAY AT(2,1):"HITS:";HITS; :: CALL COLOR(#1,7) :: CALL SOUND(-100,-2,15,110,0) :: FOR E=1 TO 25 :: NEXT E :: CALL COLOR(#1,5)
203 IF HITS=5 THEN 175 ELSE IF BOMBS THEN 186 ELSE 208
204 REM  BOMBING TOO LOW
205 CALL SOUND(-999,111,30,111,30,(ALT+270)*10,30,-8,3)
206 CALL COLOR(#4,7) :: CALL SOUND(-100,-6,0) :: CALL PATTERN(#4,112) :: CALL SOUND(-100,-6,5) :: CALL PATTERN(#4,116) :: CALL DELSPRITE(#4) :: GOTO 202
207 REM
208 CALL DELSPRITE(#2,#3,#4) :: SCORE=SCORE+BOMBS*75 :: DISPLAY AT(1,1):"SCORE:";SCORE;
209 CALL POSITION(#1,K,L) :: CALL SOUND(-4000,111,30,111,30,(ALT+270)*10,30,-8,1) :: FOR I=K TO 25 STEP-1 :: CALL LOCATE(#1,I,L) :: NEXT I
210 CALL DELSPRITE(ALL)
211 DISPLAY AT(6,4)ERASE ALL:"FUEL REMAINING: "&STR$(FUEL) :: DISPLAY AT(5,(28-LEN("YOUR SCORE: "&STR$(SCORE)))/2):"YOUR SCORE: "&STR$(SCORE)
212 FOR I=1 TO 7 :: DISPLAY AT(12,1)BEEP:"  WARNING: ENEMY CHOPPERS" :: FOR DE=1 TO 30 :: NEXT DE :: DISPLAY AT(12,1):"" :: FOR DE=1 TO 30 :: NEXT DE
213 NEXT I :: CALL CLEAR :: ECR=LEVEL+1 :: IF ECR>7 THEN ECR=7
214 DISPLAY AT(1,1):"SCORE:";SCORE; :: DISPLAY AT(1,17):"HITS:";HITS; :: DISPLAY AT(2,1):"FUEL:"; :: DISPLAY AT(2,17):"CHOPPERS:";ECR;
215 AZ=ECR :: FOR II=1 TO ECR :: DISPLAY AT(2,17):"CHOPPERS:";AZ; :: CALL SPRITE(#1,136,5,40,10,#2,100,2,INT(RND*150)+20,216)
216 CALL SOUND(-4250,-4,1,200,30,200,30,200,30) :: CALL POSITION(#1,X,Y,#2,YY,ZZ) :: IF Y>130 THEN 224 ELSE FUEL=FUEL-5*(HITS*2+LEVEL)
217 DISPLAY AT(2,1):"FUEL:";FUEL; :: IF ABS(X-YY)<7 THEN 225 ELSE IF X>170 THEN CALL LOCATE(#1,22,Y+3)ELSE IF X<22 THEN CALL LOCATE(#1,170,Y+3)
218 CALL JOYST(1,X1,Y1) :: CALL MOTION(#2,SGN(X-YY)*((YY AND 11)+9),0,#1,-Y1,3) :: CALL KEY(1,K,S)
219 IF FUEL<1 THEN 175 ELSE IF K<>18 THEN 216 ELSE FUEL=FUEL-25
220 CALL SOUND(-900,-8,0,110,30,110,30,300,30) :: CALL POSITION(#1,X,Y) :: CALL SPRITE(#3,140,7,X,Y,0,127)
221 CALL POSITION(#3,XL,YL) :: IF YL<192 THEN 221 ELSE CALL DELSPRITE(#3) :: CALL COINC(#2,XL,216,7,T) :: IF T THEN CALL SOUND(-1,-4,9)ELSE 216
222 FOR T=1 TO 2 :: CALL PATTERN(#2,108+T*4) :: FOR X=5 TO 7 :: CALL SOUND(100,-X,T*10) :: NEXT X :: NEXT T
223 CALL DELSPRITE(#2) :: SCORE=SCORE+100 :: DISPLAY AT(1,1):"SCORE:";SCORE;
224 AZ=AZ-1 :: NEXT II :: GOTO 230
225 CALL SPRITE(#3,108,7,YY,230,0,-127) :: CALL SOUND(-900,-8,1,110,30,110,30,9999,30)
226 CALL POSITION(#3,Z,Z) :: IF Z>Y THEN 226 ELSE CALL DELSPRITE(#3) :: CALL COLOR(#1,2)
227 HITS=HITS+1 :: DISPLAY AT(1,17):"HITS:";HITS; :: CALL COLOR(#1,7) :: CALL SOUND(-100,-2,15,110,0) :: FOR E=1 TO 25 :: NEXT E :: CALL COLOR(#1,5)
228 IF HITS<5 THEN 216
229 CALL COLOR(#1,2) :: GOTO 176
230 CALL DELSPRITE(ALL)
231 DISPLAY AT(6,4)ERASE ALL:"FUEL REMAINING: "&STR$(FUEL) :: DISPLAY AT(5,(28-LEN("YOUR SCORE: "&STR$(SCORE)))/2):"YOUR SCORE: "&STR$(SCORE)
232 FOR I=1 TO 7 :: DISPLAY AT(12,1)BEEP:"HEADING HOME-PREPARE TO LAND" :: FOR DE=1 TO 30 :: NEXT DE :: DISPLAY AT(12,1):"" :: FOR DE=1 TO 30 :: NEXT DE
233 NEXT I
234 CALL MAGNIFY(4) :: CALL CLEAR :: CALL COLOR(11,15,15,12,15,1)
235 DISPLAY AT(3,14):"xz" :: DISPLAY AT(4,14):"y{"
236 DISPLAY AT(5,13):"xwwz"
237 DISPLAY AT(6,13):"yww{"
238 DISPLAY AT(7,12):"xwwwwz"
239 DISPLAY AT(8,12):"ywwww{"
240 DISPLAY AT(9,11):"xwwwwwwz"
241 DISPLAY AT(10,11):"ywwwwww{"
242 DISPLAY AT(11,10):"xwwwwwwwwz"
243 DISPLAY AT(12,10):"ywwwwwwww{"
244 DISPLAY AT(13,9):"xwwwwwwwwwwz"
245 DISPLAY AT(14,9):"ywwwwwwwwww{"
246 DISPLAY AT(15,8):"xwwwwwwwwwwwwz"
247 DISPLAY AT(16,8):"ywwwwwwwwwwww{"
248 DISPLAY AT(17,7):"xwwwwwwwwwwwwwwz"
249 DISPLAY AT(18,7):"ywwwwwwwwwwwwww{"
250 DISPLAY AT(19,6):"xwwwwwwwwwwwwwwwwz"
251 DISPLAY AT(20,6):"ywwwwwwwwwwwwwwww{"
252 DISPLAY AT(21,5):"xwwwwwwwwwwwwwwwwwwz"
253 DISPLAY AT(22,5):"ywwwwwwwwwwwwwwwwww{"
254 DISPLAY AT(23,4):"xwwwwwwwwwwwwwwwwwwwwz"
255 DISPLAY AT(24,4):"ywwwwwwwwwwwwwwwwwwww{"
256 CALL SPRITE(#1,92,5,100,113,#2,124,3,8,9,#3,124,13,137,213,#4,92,2,140,113)
257 DISPLAY AT(1,1)BEEP:" PRESS FIRE BUTTON TO START"
258 CALL KEY(1,K,S) :: IF K<>18 THEN 258 ELSE DISPLAY AT(1,1):""
259 PITCH=0 :: ALT=38 :: SPEED=500 :: BONUS=1000
260 DISPLAY AT(1,1):"ALT:";ALT;"M "; :: DISPLAY AT(2,1):"PITCH:";PITCH; :: DISPLAY AT(1,15):"SPEED:";SPEED :: DISPLAY AT(2,15):"FUEL:";FUEL
261 FUEL=FUEL-((LEVEL+HITS)*5) :: IF FUEL<200 THEN DISPLAY AT(4,1):"LOW"; :: DISPLAY AT(4,25)BEEP:"FUEL" :: IF FUEL<1 THEN 288
262 CALL JOYST(1,XL,YL)
263 CALL MOTION(#1,0,XL,#4,0,XL)
264 SHIT=ALT
265 PITCH=PITCH-YL/4 :: ALT=ALT+INT(PITCH-1-(ABS(SPEED-700)/75)) :: IF ALT<1 THEN 272
266 BONUS=BONUS-LEVEL*15 :: CALL SOUND(-999,111,30,111,30,ALT*15+410,30,-8,0)
267 IF ABS(PITCH)>10 THEN 288 ELSE IF SPEED<250 THEN 288
268 IF ALT>60 THEN YL=0 :: ALT=60 :: PITCH=0
269 SPEED=SPEED-LEVEL*8 :: FUEL=FUEL-HITS*2+LEVEL*2 :: CALL MOTION(#2,SPEED/5,0,#3,SPEED/5,0)
270 CALL POSITION(#1,X,Y) :: CALL LOCATE(#1,138-ALT,Y,#4,140,Y)
271 GOTO 260
272 CALL MOTION(#1,0,0,#4,0,0) :: CALL POSITION(#1,X,Y) :: CALL LOCATE(#4,140,Y,#1,138,Y) :: IF BONUS<1 THEN BONUS=0
273 IF Y<76 OR Y>156 THEN 89 ELSE IF ABS(ALT-SHIT)>3 THEN 89
274 IF PITCH<0 THEN 288 ELSE CALL SOUND(-100,110,0) :: BONUS=BONUS+FUEL
275 FOR AI=SPEED TO 0 STEP-15 :: CALL MOTION(#2,AI/5,0,#3,AI/5,0)
276 ABC=-ABC :: IF ABC=1 THEN CALL COLOR(11,2,15)ELSE CALL COLOR(11,15,15)
277 CALL SOUND(-999,111,30,111,30,AI*20+110,30,-8,0) :: NEXT AI :: FOR DE=1 TO 150 :: NEXT DE
278 CALL CLEAR :: CALL SCREEN(6) :: CALL DELSPRITE(ALL) :: FOR I=1 TO 8 :: CALL COLOR(I,16,1) :: NEXT I
279 CALL SOUND(-1,40000,30) :: DISPLAY AT(2,9):"BONUS:";BONUS :: SCORE=SCORE+BONUS :: DISPLAY AT(3,9):"SCORE:";SCORE
280 DISPLAY AT(7,1):"SAFE LANDING...." :: IF SILOS/2>SI THEN AV=0 ELSE AV=1
281 IF AV=0 THEN 291
282 DISPLAY AT(9,10):"....MISSION SUCCESS"
283 DISPLAY AT(15,1):"YOU HAVE BEEN PROMOTED.": :"GET READY FOR A HARDER      MISSION, PILOT." :: LEVEL=LEVEL+1
284 ERUN=350 :: HITS=HITS-2 :: IF HITS<0 THEN HITS=0
285 DISPLAY AT(24,2):"PRESS FIRE BUTTON TO BEGIN"
286 CALL KEY(1,K,S) :: IF K<>18 THEN 286
287 GOTO 60
288 REM  OUT OF CONTROL
289 CALL MOTION(#1,0,0,#4,0,0) :: CALL POSITION(#1,XX,YY,#4,XXX,YYY) :: A=1350 :: FOR I=XX TO XXX-2 :: CALL LOCATE(#1,I,YY)
290 CALL SOUND(-100,A,0) :: A=A-20 :: NEXT I :: GOTO 89
291 REM
292 DISPLAY AT(9,10):"....MISSION FAILURE" :: DISPLAY AT(15,1):"YOU HAVE NOT BEED GRANTED":"ANOTHER MISSION AS YOU DID"
293 DISPLAY AT(17,1):"NOT SUCCESSFULLY COMPLETE   THIS ONE." :: IF SCORE>HISCORE THEN HISCORE=SCORE :: DISPLAY AT(5,9):"HISCORE:";HISCORE
294 DISPLAY AT(23,1)BEEP:"   PLAY AGAIN??  (Y OR N)"
295 CALL KEY(3,K,S) :: IF S=0 THEN 295
296 IF K=78 THEN CALL CLEAR :: END
297 IF K<>89 THEN CALL SOUND(-1,110,0,-2,0) :: GOTO 294
298 GOTO 58
299 CALL CLEAR :: CALL DELSPRITE(ALL) :: CALL SCREEN(6) :: FOR I=1 TO 8 :: CALL COLOR(I,16,1) :: NEXT I
300 DISPLAY AT(2,9):"SCORE:";SCORE :: IF SCORE>HISCORE THEN HISCORE=SCORE
301 DISPLAY AT(3,9):"HISCORE:";HISCORE :: DISPLAY AT(10,1):"YOU HAVE FAILED.  YOU HAVE  BEEN RESIGNED DUE TO THE    LOSS OF YOUR BOMBER."
302 !@P+
303 GOTO 294
