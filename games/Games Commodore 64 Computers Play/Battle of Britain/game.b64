10 REM *** BATTLE OF BRITAIN ***
20 GOSUB 5000:REM INITIALISE
30 GOSUB 6000:REM START
40 GOSUB 1000
50 GOSUB 1500
60 GOSUB 2000
70 GOSUB 2500
80 GOSUB 3200
90 GOSUB 3400
100 GOSUB 3700
110 GOTO 3800
1000 PRINT "{CLR}" TAB(7) "{RED}R.A.F. INTELLIGENCE REPORT{BLU}{DOWN}"
1002 REM CLR SCRN RED BLUE DOWN CRSR
1005 PRINT TAB(14) "{RVS}{RED}SEPTEMBER";DD;"{OFF}{BLU}{DOWN}"
1007 REM REV ON RED REV OFF BLUE DOWN CRSR
1010 PRINT "ESTIMATE OF SERVICEABLE ";LU$;" PLANES":PRINT
1020 PRINT B9$;"=";INT(B9*GS*(40*RND(1)+80)/100)
1030 PRINT B0$;"=";INT(B0*GS*(40*RND(1)+80)/100)
1040 PRINT HE$;"=";INT(H1*GS*(40*RND(1)+80)/100)
1050 PRINT DO$;" =";INT(D7*GS*(40*RND(1)+80)/100)
1060 PRINT JU$;" =";INT(J8*GS*(40*RND(1)+80)/100)
1070 PRINT ST$;" = ";INT(J7*GS*(40*RND(1)+80)/100):PRINT
1080 PRINT TAB(8) "{RED}11 GROUP FROCES AVAILABLE{BLU}":PRINT
1085 REM RED BLUE
1090 PRINT SP$;" =";INT(SP*BS/100)
1100 PRINT HU$;"=";INT(HU*BS/100)
1110 PRINT BF$;"  =";BF
1120 PRINT:PRINT "R.A.F. AIRFIELD SERVICEABILITY=";BS;"%"
1130 X=9:Y=22:GOSUB 5300
1140 GOSUB 5260:RETURN
1150 PRINT "{BLK}{CLR}{6 DOWN}{RIGHT}THE ";LU$; " IS PLANNING ITS ATTACKS{BLU}"
1505 REM BLACK CLR SCRN DOWN CRSR RIGHT CRSR BLUE
1510 GC=INT(2000*RND(1)+1)+(LL*2)+CF
1520 IF GC<300 THEN GT=1:GC$="RADAR STATIONS":GOTO 1580
1530 IF GC<1200 THEN GT=3:GC$="FIGHTER AIRFIELDS":GOTO 1580
1540 IF GC<1500 THEN GT=4:GC$="FIGHTER PRODUCTION FACTORIES":GOTO 1580
1550 IF GC<1800 THEN GT=2:GC$="ENGLISH CHANNEL POSTS":GOTO 1580
1560 IF GC<2001 THEN GT=5:GC$="SMALL INDUSTRIAL TOWNS":GOTO 1580
1570 GT=6:GC$="LONDON"
1580 WF=150-INT(100*RND(1)+1):IF WF>100 THEN WF=100
1590 GOSUB 5210:RETURN
2000 PRINT "{CLR}":PRINT TAB(7) "{RVS}{RED}11 GROUP DEPLOYMENT CHOICE{BLU}{DOWN}":GOSUB 5220
2005 REM CLR SCRN REV ON RED BLUE DOWN CRSR
2010 PRINT "{DOWN}WEATHER REPORT:-";100-WF;"% CLOUD COVER{DOWN}"
2015 REM DOWN CRSR
2020 PRINT LU$;" LOSSES =";LL:PRINT
2030 PRINT "THE ";LU$;" HAS ABANDONED ATTACKS ON:"
2040 IF (LL*2)+CF<301 THEN PRINT "NOTHING"
2050 IF (LL*2)+CF>300 THEN PRINT "RADAR STATIONS"
2060 IF (LL*2)+CF>1199 THEN PRINT "11 GROUP FIGHTER AIRFIELDS"
2070 IF (LL*2)+CF>1799 THEN PRINT "CHANNEL PORTS"
2080 IF (LL*2)+CF>2000 THEN PRINT "SMALL INDUSTRIAL TOWNS"
2090 X=1:Y=11:GOSUB 5300
2100 PRINT "WHERE WILL YOU CONCENTRATE 11 GROUP'S","DEFENCES?":PRINT
2110 PRINT "1=RADAR STATIONS","4=FIGHTER FACTORIES"
2120 PRINT "2=CHANNEL PORTS","5=SMALL TOWNS"
2130 PRINT "3=FIGHTER AIRFIELDS","6=LONDON"
2150 INPUT D
2160 IF D<1 OR D>6 THEN GOTO 2150
2170 GOSUB 5220
2180 PRINT "{CLR}":X=1:Y=3:GOSUB 5300
2185 REM CLR SCRN
2190 PRINT "YOU HAVE CHOSEN TO DEFEND"
2200 IF D=1 THEN PRINT "THE RADAR STATIONS"
2210 IF D=2 THEN PRINT "THE CHANNEL PORTS"
2220 IF D=3 THEN PRINT "THE FIGHTER AIRFIELDS"
2230 IF D=4 THEN PRINT "THE FIGHTER FACTORIES"
2240 IF D=5 THEN PRINT "THE SMALL INDUSTRIAL TOWNS"
2250 IF D=6 THEN PRINT "LONDON"
2260 GOSUB 5230
2270 PRINT:PRINT "THE ";LU$;" IS CONCENTRATING ITS"
2280 PRINT "ATTACK ON ";GC$:PRINT
2290 IF GT=D THEN IR=100
2300 IF GT-D=1 OR GT-D=-1 THEN IR=80
2310 IF GT-D=2 OR GT-D=-2 THEN IR=60
2320 IF GT-D=3 OR GT-D=-3 THEN IR=40
2330 IF GT-D>3 OR GR-D<-3 THEN IR=20
2340 PRINT IR;"% OF YOUR FIGHTERS INTERCEPT":PRINT:GOSUB 5220
2350 IF IR=100 THEN PRINT "11 GROUP HAS BEEN BRILLIANTLY DEPLOYED!"
2360 IF IR=80 THEN PRINT "11 GROUP HAS BEEN WELL DEPLOYED"
2370 IF IR=60 THEN PRINT "11 GROUP FACES HEAVY ODDS"
2380 IF IR=40 THEN PRINT "11 GROUP HAS BEEN POORLY DEPLOYED"
2390 IF IR=20 THEN PRINT "11 GROUP HAS BEEN TOTALLY OUTWITTED"
2400 GOSUB 5200:RETURN
2500 ZY=INT(SP*BS*WF/1000000*IR)+1
2510 KF=12:AZ$=SP$:BT=1
2520 GOSUB 2900
2530 ZY=INT(HU*BS*WF/1000000*IR)+1
2540 KF=7:AZ$=HU$:BT=1
2550 GOSUB 2900
2560 ZY=INT(BF*GT*WF/600)+1
2570 KF=3:AZ$=BF$:BT=1
2580 GOSUB 2900
2590 ZY=INT(B9*GS*WF/100*.65)+1
2600 KF=14:AZ$=B9:BT=0
2610 GOSUB 2900
2620 ZY=INT(B0*GS*WF/100*.75)+1
2630 KF=5:AZ$=B0$:BT=0
2640 GOSUB 2900
2650 ZY=INT(H1*GS*WF/100*.4)+1
2660 KF=2:AZ$=HE$:BT=0
2670 GOSUB 2900
2680 ZY=INT(D7*GS*WF/100*.4)+1
2690 AZ$=DO$:BT=0
2700 GOSUB 2900
2710 ZY=INT(J7*GS*WF/100*.3)+1
2720 AZ$=ST$:BT=0
2730 GOSUB 2900
2740 ZY=INT(J8*GS*WF/100*.4)+1
2750 KF=3:AZ$=JU$:BT=0
2760 GOSUB 2900
2770 RETURN
2900 PRINT "{CLR}
2905 REM CLR SCRN
2910 PRINT "{RED}"ZY;AZ$;"S ATTACK{BLU}":GOSUB 5220
2915 REM RED BLUE
2920 FOR ZZ=1 TO ZY
2930 IF INT(100*RND(1))+1=<KF THEN KI=1
2940 IF KI=0 THEN PRINT AZ$;ZZ;"......"
2950 IF KI=1ANDBT=1 THEN GOSUB 3000:PRINT AZ$;ZZ;"{RED}";BZ$;"{BLU}":GOSUB 5500:PH=PH+1
2960 IF KI=1ANDBT=0 THEN GOSUB 3080:PRINT AZ$;ZZ;"{RED}";BZ$;"{BLU}":GOSUB 5500:PH=PH+1
2970 KI=0:GOSUB 5240:NEXT ZZ
2980 PRINT "{CLR}":PRINT "{RED}"AZ$;"S DESTROY";PH;"PLANES{BLU}"
2985 REM CLR SCRN RED BLUE
2990 PH=0:GOSUB 5200:RETURN
3000 SD=INT(100*RND(1))+1
3010 IF SD<20 THEN D7=D7-1:BZ$=DO$:L1=L1+1:GOTO 3070
3020 IF SD<44 THEN H1=H1-1:BZ$=HE$:L2=L2+1:GOTO 3070
3030 IF SD<61 THEN J8=J8-1:BZ$=JU$:L3=L3+1:GOTO 3070
3040 IF SD<71 THEN J7=J7-1:BZ$=ST$:L4=L4+1:GOTO 3070
3050 IF SD<84 THEN B9=B9-1:BZ$=B9$:L5=L5+1:GOTO 3070
3060 B0=B0-1:BZ$=B0$:L6=L6+1
3070 LL=LL+1:L0=L0+1:RETURN
3080 SD=INT(100*RND(1))+1
3090 IF SD<76 THEN HU=HU-1:BZ$=HU$:L7=L7+1:GOTO 3110
3100 SP=SP-1:BZ$=SP$:L8=L8+1
3110 L9=L9+1:RETURN
3200 PRINT "{CLR}"
3205 REM CLR SCRN
3210 PRINT "{RVS}{RED}AIR LOSSES-SEPTEMBER";DD
3215 REM REV ON RED
3220 PRINT "{DOWN}{RVS}R.A.F.{DOWN}{BLU}"
3225 REM DOWN CRSR REV ON DOWN CRSR BLUE
3230 PRINT SP$;"  =";L8
3240 PRINT HU$;" =";L7
3245 PRINT "{DOWN}{RED}TOTAL     =";L9;"{BLU}"
3250 PRINT "{DOWN}{RVS}{BLK}";LU$;"{DOWN}{BLU}"
3255 REM DOWN CRSR REV ON BLACK DOWN CRSR BLUE
3260 PRINT HE$;"=";L1
3270 PRINT DO$;" =";L2
3280 PRINT JU$;" =";L3
3290 PRINT ST$;" =";L4
3300 PRINT B9$;"=";L5
3310 PRINT B0$;"=";L6
3320 PRINT "{DOWN}{RED}TOTAL  =";L0;"{BLU}"
3325 REM DOWN CRSR RED BLUE
3330 X=8:Y=22:GOSUB 5300:GOSUB 5260
3340 L0=0:L1=0:L2=0:L3=0:L4=0
3350 L5=0:L6=0:L7=0:L8=9:L9=0
3360 RETURN
3400 PRINT "{CLR}"
3405 REM CLR SCRN
3410 PRINT "{DOWN}THE LUFTWAFFE ATTACK"
3412 REM DOWN CRSR
3415 PRINT "{RVS}{RED};GC$;"{BLU}"
3417 REM REV ON RED BLUE
3420 GOSUB 5220:X=1:Y=6:GOSUB 5300
3430 IF GT=1 THEN GOTO 3480
3440 IF GT=2 THEN GOTO 3530
3450 IF GT=3 THEN GOTO 3560
3460 IF GT=4 THEN GOTO 3620
3470 IF GT>4 THEN GOTO 3640
3480 ZZ=INT((H1+D7+J8+(J7*2))*WF*WF*GS/500000)
3490 BS=BS-ZZ
3500 PRINT "R.A.F. AIRFIELD SERVICEABILITY IS REDUCED ";
3510 PRINT "TO";BS;"%"
3520 VP=VP+ZZ:GOTO 3660
3530 ZZ=INT((H1+D7+J8+J7)*WF*WF*GS/1000000)
3540 VP=VP+ZZ:GOTO 3660
3560 KF=101-INT(BS*1.4):IF KF<1 THEN KF=1:AZ$="GERMAN BOMBER":BT=0:ZY=ZZ
3565 AZ$="GERMAN BOMBER":ZY=ZZ:BT=0
3570 GOSUB 5220:GOSUB 2900
3580 ZZ=INT(ZZ/10):BS=BS-ZZ
3590 PRINT "R.A.F. AIRFIELD SERVICEABILITY IS REDUCED ";
3600 PRINT "TO";BS;"%"
3610 VP=VP+ZZ:GOTO 3660
3620 ZZ=INT((H1+D7+J8+J7)*WF*WF*GS/1000000)
3625 IF INT(100*RND(1)+1)<WF THEN SR=SR-1:HR=HR-3:X=1
3630 IF X=1 THEN PRINT "PRODUCTION DAMAGED!":X=0:VP=VP+ZZ:GOTO 3660
3640 ZZ=INT((H1+D7+J8)*WF*WF*GS/1000000)
3650 VP=VP+ZZ
3660 X=1:Y=12:GOSUB 5300
3670 PRINT ZZ;"VICTORY POINTS ARE AWARDED TO THE ",;LU$
3680 GOSUB 5200:RETURN
3700 H1=H1+GR:D7=D7+GR:J8=J8+GR
3710 J7=J7+GR:B9=B9+GR:B0=B0+GR
3720 SP=SP+SR:HU=HU+HR:DD=DD+1
3730 IF GT<>1 AND GT<>3 THEN BS=BS+2
3740 RETURN
3800 PRINT "{CLR}"
3805 REM CLR SCRN
3810 IF RND(1)> .8 THEN GOTO 3860
3820 PRINT "BOMBER COMMAND RAIDS FRENCH PORTS"
3830 PRINT "ATTEMPTING TO SINK INVASION BARGES!"
3840 GOSUB 5220:PRINT "{DOWN}1 VICTORY POINT IS AWARDED TO THE R.A.F."
3845 REM DOWN CRSR
3850 VP=VP-1:GOTO 3910
3860 PRINT "THE PRIME MINISTER HAS ORDERED BOMBER"
3870 PRINT "COMMAND TO LAUNCH REPRISAL RAIDS AGAINST"
3880 PRINT "BERLIN!"
3890 PRINT "{DOWN}THE FUHRER IS FURIOUS"
3900 CF=CF+100
3910 GOSUB 5200:IF DD<15 THEN GOTO 40
3920 PRINT "{CLR}"
3925 REM CLR SCRN
3930 PRINT "{RVS}{RED}THE GAME IS OVER{BLU}"
3935 REM REV ON RED BLUE
3940 IF VP<46 THEN A$="R.A.F.":B$="TACTICAL"
3950 IF VP<56 THEN B$="TOTAL"
3970 IF VP>55 AND VP<65 THEN GOTO 4020
3980 IF VP>64 THEN A$=LU$:B$="TACTICAL"
3990 IF VP>74 THEN B$="TOTAL"
4000 X=1:Y=10:GOSUB 5300
4010 PRINT "THE ";A$;" HAVE WON A ";B$;" VICTORY":GOTO 4040
4020 PRINT "THE GAME IS A DRAW"
4030 PRINT "NEITHER SIDE HAS WON AN ADVANTAGE"
4040 PRINT "THE ";LU$;" SCORED";VP;"POINTS"
4050 STOP
5000 HE$="HE 111":DO$="DO 17"
5010 JU$="JU 88":ST$="JU 87"
5020 B9$="BF 109":BO$="BF 110"
5030 SP$="SPITFIRE":HU$="HURRICANE"
5040 BF$="A.A.GUN":LU$="LUFTWAFFE"
5050 HU=560:SP=190:BF=100:HR=12
5060 D7=300:H1=400:J8=270:SR=3
5070 J7=150:B9=330:B0=230:GR=2
5080 GS=.55:BS=75:DD=1
5090 X$="{HOME}{22 RIGHT}"
5095 REM HOME CCRSR RIGHT CRSR
5100 Y$="{23 DOWN}"
5105 REM DOWN CRSR
5110 RETURN
5200 FOR TD=1 TO 1000:NEXT TD
5210 FOR TD=1 TO 500:NEXT TD
5220 FOR TD=1 TO 400:NEXT TD
5230 FOR TD=1 TO 90:NEXT TD
5240 FOR TD=1 TO 10:NEXT TD
5250 RETURN
5260 PRINT "{RVS}{RED}HIT ANY KEY TO CONTINUE{BLU}"
5265 REM REV ON RED BLUE
5270 GET A$
5280 IF A$="" THEN GOTO 5270
5290 RETURN
5300 PRINT LEFT$(X$,X);LEFT$(Y$,Y);:RETURN
5500 REM SOUND ROUTINE
5510 POKE 54296,15:POKE 54277,24:POKE 54278,134:POKE 54276,17
5520 POKE 54273,68:POKE 54272,149
5530 FOR TD=1 TO 50:NEXT TD
5540 POKE 54296,0:POKE 54276,0
5500 RETURN
6000 PRINT "{CLR}":REM CLR SCRN
6010 POKE 53280,0:POKE 53281,14
6020 X=12:Y=10:GOSUB 5300
6030 PRINT "{RED}BATTLE {WHT}OF {BLU}BRITAIN"
6035 REM RED WHITE BLUE
6040 POKE 54296,15
6050 POKE 54277,132:POKE 54278,194:POKE 54276,33
6060 FOR HF=10 TO 45:FOR LF=2 TO 140 STEP 4
6070 POKE 54273,HF:POKE 54272,LF
6080 NEXT:NEXT
6090 FOR TD=1 TO250:NEXT
6100 POKE 54296,0:POKE 54276,0
6110 POKE 53280,15:RETURN
