10 REM *** DESCENT INTO CHAOS ***
20 REM *** JOYSTICK VERSION ***
40 GOSUB 1600:GOSUB 1800
50 POKE 53280,0:POKE 53281,0:DD=56320
60 PRINT "{CLR}":S$="{UP}        "
65 REM CLR SCRN UP CRSR 8 SPACES
70 Q=7:SC=1024:X=14
80 FOR RR=1 TO 200:PRINT "{RVS}{BLU}                                                "
82 REM REV ON BLUE 38 SPACES
85 IF S$="{UP}  " THEN 1500:REM UP CRSR 2 SPACES
90 Z=RND(1)
100 Q=Q+(Z<.5)-(Z>=.5)
110 IF Q>24 THEN Q=24
120 IF Q<=4 THEN Q=4
130 PRINT TAB(Q+3) S$
140 POKE SC+X,90:POKE SC+54272+X,1
150 X1=X:GOSUB 5000:IF FR<>16 THEN FOR TD=1 TO 300:NEXT
155 IF JV=4 THEN X=X-1
157 IF JV=8 THEN X=X+1
170 IF PEEK(SC+X+40)=160 THEN 1000
180 M=M+10:NEXT:S$=LEFT$(S$,(LEN(S$)-1)):GOTO 80
1000 H=H+1
1010 POKE VO,15:POKE AD,17:POKE SR,164:POKE WF,33:POKE HF,3:POKE LF,155
1020 FOR TD=1 TO 500:NEXT TD:POKE VO,0:POKE WF,0
1030 FOR TD=1 TO 1000:NEXT TD:IF H=4 THEN GOTO 1200
1050 IF PEEK(SC+X-2)=32 THEN X=X-2:GOTO 1070
1060 X=X+2
1070 GOTO 180
1200 PRINT "{CLR}":PRINT TAB(10) "{C1}{6 DOWN}{RVS}**** GAME OVER ****"
1205 REM ORANGE DOWN CRSR
1210 PRINT TAB(2) "{DOWN}{C7}YOU PENETRATED";M/1000;"KILOMETRES INTO"
1215 REM DOWN CRSR LT BLUE
1220 PRINT TAB(9) "{PUR}{DOWN}THE TUNNELS OF CHAOS"
1225 REM PURPLE DOWN CRSR
1230 POKE VO,15:POKE AD,15:POKE WF,17
1240 FOR H1=60 TO 20 STEP -2:FOR L1=250 TO 10 STEP -5
1250 POKE HF,H1:POKE LF,L1:NEXT:NEXT
1260 POKE VO,0:POKE WF,0
1270 FOR TD=1 TO 1000:NEXT
1280 POKE 53280,11:POKE 53281,6:PRINT "{CLR}"
1290 PRINT TAB(10) "{5 DOWN}ANOTHER GAME (Y/N)"
1295 REM DOWN CRSR
1300 GET A$:IF A$="" THEN 1300
1310 IF A$="Y" THEN RUN
1320 IF A$="N" THEN END
1330 IF A$<>"Y" AND A$<>"N" THEN 1300
1500 PRINT "{CLR}"
1505 REM CLR SCRN
1510 POKE VO,15:POKE AD,17:POKE SR,32:POKE WF,17
1520 H1=INT(20*RND(1)+2):L1=INT(40*RND(1)+10)
1530 POKE HF,H1:POKE LF,L1:FOR TD=1 TO 50:NEXT TD
1540 SS=SS+1:IF SS>8 THEN SS=0:POKE VO,0:POKE WF,0:GOTO 1560
1550 GOTO 1520
1560 PRINT TAB(12) "{3 DOWN}{RVS}{YEL}WELL DONE!!"
1565 REM DOWN CRSR YELLOW
1570 PRINT TAB(4) "{DOWN}YOU MADE IT TO THE BOTTOM OF THE"
1575 REM DOWN CRSR
1580 REM DOWN CRSR ORANGE
1590 GOTO 1210
1600 PRINT"{CLR}"
1605 REM CLR SCRN
1610 VO=54296:AD=54278:SR=54277:WF=54276:HF=54273:LF=54272
1700 RETURN
1800 PRINT"{CLR}":POKE 53280,11:POKE 53281,14:REM CLR SCRN
1810 GOSUB 2000:PRINT "{HOME}{8 DOWN}":PRINT TAB(10) "DESCENT INTO CHAOS"
1815 REM HOME DOWN CRSR
1820 GOSUB 2010:PRINT "{HOME}{8 DOWN}":PRINT TAB(10) "DESCENT INTO CHAOS"
1830 GOSUB 2020:PRINT "{HOME}{8 DOWN}":PRINT TAB(10) "DESCENT INTO CHAOS"
1840 POKE VO,15:POKE AD,17:POKE SR,132:POKE WF,33
1850 H1=100:L1=200
1860 POKE HF,H1:POKE LF,L1:FOR TD=1 TO 125:NEXT
1870 H1=H1-5:L1=L1-10:XX=XX+1:IF XX=17 THEN XX=0:POKE VO,0:POKE WF,0:RETURN
1880 GOTO 1860
2000 FOR TD=1 TO 500:NEXT:PRINT "{RED}":RETURN
2010 FOR TD=1 TO 500:NEXT:PRINT "{BLU}":RETURN
2020 FOR TD=1 TO 500:NEXT:PRINT "{BLK}":RETURN
5000 JV=PEEK(DD):FR=JV AND 16:JV=15-(JVAND15)
5010 RETURN
