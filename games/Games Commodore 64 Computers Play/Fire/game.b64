1 DIM S1(39):V=53248:HI=0:X$="NO ONE"
2 RESTORE:GOSUB 3000:POKE 53280,0:SC=0
3 DATA 1,2,2,2,2,1,3,3,3,3:REM THIS
4 DATA 1,2,2,2,2,1,2,2,2,1:REM DATA
5 DATA 1,3,3,3,3,3,3,3,3,3:REM IS
6 DATA 1,4,4,4,4,1,2,2,2,1:REM FOR
7 DATA 1,4,4,4,4,1,3,3,3,1:REM THE
8 DATA 2,2,2,2,2,1,3,3,3,3:REM COUNT-
9 DATA 1,3,3,3,3,1,3,3,3,1:REM DOWN
10 DATA 1,3,3,3,3,1,4,4,4,1
11 DATA 5,5,5,5,5,5,5,5,5,5,1,2,2,2,2,2,2,2,3,1
12 X=175:Y=195:P=195:SY=50:SP=SY:SM=SX:M=0:N=0:SE=1
23 FOR A=1 TO 70:K=INT(RND(1)*15):PRINT SPC(K);".";:NEXT A
24 IF BG=0 THEN FOR A=0 TO 39:READ S1(A):NEXT:GOTO 41
25 FOR A=0 TO 39
26 READ Y1:S1(A)=Y1
27 FOR B=Y1 TO 19:POKE (55296+A+40*B),12
28 POKE (1024+A+40*B),93
30 NEXT
32 POKE (1024+A+40*Y1),100
40 NEXT
41 POKE 1164,81: POKE 55436,7
92 DATA 13,14,15,16,17,16,14,13,12,11,10,9,8,8,7,7,7,8,9,10,11,11,12,13,13,14
93 DATA 14,14,13,12,11,11,10,9,9,8,7,6,7,8
100 V=53248
130 POKE 2041,15:REM S-1=ALIEN BOMB
140 POKE 2042,13:REM S-2=ALIEN
150 POKE 2043,14:REM S-3=YOUR BASE
160 POKE 2044,15:REM S-4=YOUR MISSILE
170 POKE 2045,11:REM S-5=EXPLOSION
200 FOR N=0 TO 62:READ Q:POKE 832+N,Q:NEXT:REM POKEING DATA FOR ALIEN
210 DATA 0,0,0,0,90,0,0,255,0,1,129,128, 15,255,240,56,0,28,127,255,254
220 DATA 79,231,242,96,0,6,100,146,86,48,0,12,15,255,240,15,60,240
230 DATA 0,219,0,1,16,128,2,16,64,4,16,32,8,16,16,4,8,32,2,8,64,4,8,32
300 FOR N=0 TO 62:READ Q:POKE 896+N,Q:NEXT:REM POKEING DATA FOR YOUR BASE
320 DATA 0,0,0,0,0,0,0,129,0
330 DATA 1,129,128,0,129,0,0,129,0
340 DATA 1,129,128,3,0,192,3,129,192
350 DATA 15,0,240,31,0,248,43,0,212
360 DATA 33,255,132,33,255,132,34,255,68,31,126,248,39,189,228,47,214,244
370 DATA 31,231,248,63,231,252,127,195,254
375 FOR N=704 TO 766:POKE N,255:NEXT:REM POKEING DATA FOR EXPLOSION
400 FOR A=0 TO 62:READ D:POKE 960+A,D:NEXT
410 DATA 0,24,0,0,24,0,0,36,0
420 DATA 0,66,0,0,126,0,0,126,0
430 DATA 0,126,0,0,66,0,0,66,0
440 DATA 0,60,0,0,60,0,0,36,0
458 DATA 0,36,0,0,60,0,0,60,0
460 DATA 0,0,0,0,0,0,0,74,0
470 DATA 0,82,0,0,36,0,0,24,0
473 POKE V+21,28:POKE V+41,1:POKE V+42,8:POKE V+43,14:POKE V+7,Y:N=0
498 POKE V+23,12:B=10:SI=0.5
500 I=PEEK(197)
501 PRINT "{HOME}SCORE ";SC:REM "{HOME}" IS HOME
510 IF I=34 THEN X=X-B:GOTO530
520 IF I=37 AND X<244 THEN X=X+B:GOTO530
525 IF I=21 AND N=0 THEN P=P-1:L=X:N=2
530 IF P<195 AND P>10 THEN P=P-20:GOTO600
535 IF P<20 THEN L=X:N=0:P=195
560 IF P>190 THEN L=X
600 IF SX<X-10 OR SX>X+10 OR M=2 THEN 710
620 SP=SY:M=2:POKE V+21,30:SM=SX
710 IF M=2 THEN SP=SP+15
711 IF SP>220 OR M=0 THEN M=0:SP=SY:SM=SX:POKE V+21,28
740 IF X>255 THEN X=X-10
741 IF X<30 THEN X=X+10
750 POKE V+6,X
760 IF P<196 AND P>20 THEN POKE V+9,P
770 POKE V+8,L
771 O=SI*B:IF Q=2 THEN O=-O
772 SX=SX+O
775 IF SX>254 THEN SY=SY+15:Q=2:SX=254
776 IF SX<30 THEN Q=0:SX=30:SY=SY+13
780 POKE V+4,SX: POKE V+5,SY
781 POKE V+2,SM: POKE V+3,SP
790 IF PEEK(V+30)=18 AND P<>195 THEN 800
791 IF PEEK(V+30)=12 OR PEEK(V+30)=28 OR PEEK(V+30)=30 THEN 2000
795 IF PEEK(V+30)=20 OR PEEK(V+30)=22 THEN 900
796 IF PEEK(V+30)=10 OR PEEK(V+30)=18 AND P=195 OR PEEK(V+30)=26 THEN 2000
797 GOTO 850
800 POKE V+40,2:POKE V+21,62:POKE V+10,L:POKE V+11,P
810 P=195:L=X:SP=SY:M=0:N=0:POKE V+44,1
820 FOR A=1 TO 100:NEXT:POKE V+21,30
830 POKE V+44,1
850 GOTO 500
900 IF L>SX-2 AND L<SX+2 THEN 909
901 POKE V+10,L:POKE V+11,SY:POKE V+21,62
902 FOR A=1 TO 50
903 POKE V+44,1:POKE V+44,10
904 NEXT A:POKE V+21,14
907 SC=SC+10
908 GOTO 500
909 IF L=SX THEN SC=SC+100
910 POKE V+21,0
911 PRINT"{CLR}":FOR A=1 TO 30
912 REM CLR SCRN
920 IF A/2=INT(A/2) THEN C=2:B=0:GOTO 940
930 C=0:B=2
940 POKE 53280,C
950 POKE 53281,B
960 NEXT
965 SC=SC+100
980 POKE 53280,0:POKE 53281,0
1000 X=175:Y=195:P=195:SY=50:SX=20:SP=SY:SM=SX:M=0:N=0:Q=0:SE=SE+1
1001 FOR A=1 TO 70:K=INT(RND(1)*15):PRINT SPC(K);".";:NEXT A
1002 FOR TY=1 TO SE:WE=INT(388*RND(1)+1024):POKE WE,81:POKE WE+54272,7:NEXT TY
1003 SI=SI+0.5
1004 IF BG=0 THEN 1010
1005 FOR A=0 TO 39:FOR B=S1(A) TO 19
1007 POKE (1024+A+40*B),224
1008 POKE (55296+A+40*B),3:NEXT
1009 POKE (1024+A+40*S1(A)),88:NEXT
1010 B=10:GOTO 500
2000 POKE V+21,0:PRINT"{CLR}{YEL}{3 DOWN}":REM CLR YELLOW DOWN CRSR
2020 PRINT "{C+} {C+} {C+} {C+} {C+}{C+}{C+}{C+} {C+}{C+}{C+}{C+} {C+}{C+}{C+}{C+} {C+}  {C+} {C+}{C+}{C+}{C+}{C+} {C+}"
2030 PRINT "{C+} {C+} {C+} {C+} {C+}  {C+} {C+}    {C+}  {C+} {C+}  {C+}   {C+}   {C+}"
2040 PRINT "{C+} {C+} {C+} {C+} {C+}  {C+} {C+}    {C+}  {C+} {C+}  {C+}   {C+}   {C+}"
2050 PRINT "{C+} {C+} {C+} {C+} {C+}{C+}{C+}{C+} {C+}{C+}{C+}{C+} {C+}  {C+} {C+}  {C+}   {C+}   {C+}"
2060 PRINT "{C+} {C+} {C+} {C+} {C+}    {C+}    {C+}  {C+} {C+}  {C+}   {C+}   {C+}"
2070 PRINT "{C+} {C+} {C+} {C+} {C+}    {C+}    {C+}  {C+} {C+}  {C+}   {C+}    "
2080 PRINT "{C+}{C+}{C+}{C+}{C+} {C+} {C+}    {C+}{C+}{C+}{C+} {C+}{C+}{C+}{C+} {C+}{C+}{C+}{C+}   {C+}   {C+}:
2085 REM THE ABOVE CHARACTER IS CHR$(166)
2090 PRINT:PRINT:PRINT SPC(15);"{RVS}{BLU}GAME OVER{WHT}{OFF}":REM RVS ON BVLUE WHITE REVS OFF
2091 PRINT:PRINT TAB(10);"HI SCORE:";HI:PRINT TAB(10);"HELD BY:";X$
2100 PRINT"{3 DOWN}":REM DOWN CRSR
2110 PRINT TAB(10);"{YEL}YOUR SCORE ";SC:REM YELLOW
2111 IF SC<=HI THEN 2120
2112 PRINT TAB(10);"{PUR} CONGRATULATIONS!!!": REM PURPLE
2113 PRINT TAB(10);"{YEL}YOU'RE THE NEW CHAMP":PRINT TAB(10);"WHAT IS YOUR NAME";
2114 REM YELLOW
2115 INPUT X$:HI=SC
2120 FOR Z=1 TO 500:NEXT
2130 PRINT"{CLR}{WHT}":REM CLR WHITE
2140 PRINT"   DO YOU WISH TO:":PRINT
2150 PRINT"    (1)..END GAME"
2160 PRINT"    (2)..PLAY AGAIN"
2170 GET K$:IF VAL(K$)=1 THEN END
2180 IF VAL(K$)=2 THEN GOTO 2
2190 GOTO 2170
3000 PRINT "{CLR}"
3010 REM CLR SCRN
3100 POKE 53280,0:POKE 53281,0
3155 PRINT "{CLR}"
3160 REM CLR SCRN
3500 N$(1)= "{RVS}          "
3505 REM REV ON
3510 N$(2)= "{RVS} {8 RIGHT} "
3515 REM RIGHT CRSR
3520 N$(3)= "{RVS}{9 RIGHT} "
3530 N$(4)= "{RVS} {9 RIGHT}"
3540 N$(5)= "{RVS}{4 RIGHT} {5 RIGHT}"
3545 FOR A=9 TO 0 STEP -1
3560 PRINT:PRINT:PRINT:PRINT:PRINT:PRINT:PRINT"{WHT}"
3565 REM WHITE
3570 FOR Z=1 TO 10
3580 READ F:PRINT TAB(15);"{C1}";N$(F)
3585 REM ORANGE
3590 NEXT
3600 PRINT "{CLR}":REM CLR SCRN
3601 FOR Q=1 TO 100:NEXT
3610 NEXT
3615 FOR Q=1 TO 100:NEXT
3620 PRINT "{CLR}"
3625 REM CLR SCRN
3630 PRINT:PRINT:PRINT:PRINT:PRINT"{YEL}"
3635 REM YELLOW
3640 PRINT "    {C+}{C+}{C+}{C+}{C+}{C+}  {C+}{C+}  {C+}{C+}{C+}{C+}{C+}   {C+}{C+}{C+}{C+}{C+}{C+} {C+} {C+}"
3650 PRINT "    {C+}{C+}      {C+}{C+}  {C+}    {C+}  {C+}{C+}     {C+} {C+}"
3660 PRINT "    {C+}{C+}{C+}{C+}{C+}{C+}  {C+}{C+}  {C+}{C+}{C+}{C+}{C+}   {C+}{C+}{C+}{C+}{C+}{C+} {C+} {C+}"
3670 PRINT "    {C+}{C+}      {C+}{C+}  {C+}    {C+}  {C+}{C+}     {C+} {C+}"
3680 PRINT "    {C+}{C+}      {C+}{C+}  {C+}    {C+}  {C+}{C+}     {C+} {C+}"
3690 PRINT "    {C+}{C+}      {C+}{C+}  {C+}    {C+}  {C+}{C+}{C+}{C+}{C+}{C+}    "
3700 PRINT "    {C+}{C+}      {C+}{C+}  {C+}    {C+}  {C+}{C+}{C+}{C+}{C+}{C+} {C+} {C+}"
3701 PRINT:PRINT:PRINT TAB(10);"J..LEFT      K..RIGHT"
3702 PRINT:PRINT TAB(10);"      F..FIRE      ":PRINT
3710 FOR A=1 TO 30:IF A/2=INT(A/2) THEN C=2:B=0:GOTO 3720
3715 C=0:B=10
3720 POKE 53280,C:POKE 53281,B
3740 NEXT
3745 PRINT "      DO YOU WANT A BACKGROUND?"
3746 GET N$:IF N$="Y" THEN BG=1:GOTO 3749
3747 IF N$="N" THEN BG=0:GOTO 3749
3748 GOTO 3746
3749 PRINT "{CLR}": REM CLR SCRN
3750 RETURN
