10 REM *** ROMAN EMPIRE ***
20 GOSUB 200:REM INITIALIZE
30 GOSUB 7000:REM START
40 GOSUB 500:REM ALLOCATE LAOR
50 GOSUB 830:REM HIRE AUXILIA
60 GOSUB 1150:REM SHIPBUILDING
70 GOSUB 1370:REM TRADE ALLOCATION
80 GOSUB 1530:REM WARS
90 GOSUB 1800:REM WIN OR LOSE/LOSSES
100 GOSUB 2410:REM PIRACY
110 GOSUB 2550:REM TRADE
120 GOSUB 2700:REM FEED THE ROMANS
130 GOSUB 2870:REM POLITICAL SEGMENT
140 GOTO 3110:REM END TURN
200 PRINT "{CLR}":POKE 53280,0:POKE 53281,0:TR=50000:TN=-55:PO=100000:LP=250
205 REM CLR SCRN
210 MS=INT(20*RND(1))+111
220 WS=INT(10*RND(1))+30
230 FS=INT(10*RND(1))+10
240 VL=100:FR=100:FL=1
250 LA=INT(10*RND(1))+6
260 MA=INT(6*RND(1))+5
270 LE=INT(10*RND(1))+11
280 MC=2000:LN=WS:LM=MS:GV=100
290 LS=INT(30*RND(1))+16
300 UL=250-LS-LE-LM-LN-LA
310 IF UL<0 THEN GOTO 210
320 X$="{HOME}{25 RIGHT}"
325 REM HOME RIGHT CRSR
330 Y$="{24 DOWN}"
335 REM DOWN CRSR
340 VO=54296:AD=54277:SR=54278:WF=54276:HF=54273:LF=54272
350 RETURN
500 IF FL=0 THEN PRINT "{CLR}CHECKING LABOUR REQUIREMENTS"
505 REM CLR SCRN
510 FOR TD=1 TO 200:NEXT:FL=0
520 IF UL<>0 THEN GOSUB 5800
530 IF UL=0 THEN PRINT "{CLR}{3 DOWN}{5 RIGHT}{C7}{RVS}YOU HAVE NO LABOUR TO ALLOCATE{YEL}"
535 REM CLR SCRN DOWN CRSR RIGHT CRSR LT BLUE REV NO
540 IF UL=0 THEN GOSUB 6010
550 IF UL=0 THEN RETURN
560 PRINT "{CLR}{DOWN}LABOUR TO ALLOCATE =";UL
565 REM CLR DOWN CRSR
570 PRINT "{DOWN}WHERE DO YOU WISH TO EMPLOY THEM?{2 DOWN}"
575 REM DOWN CRSR
580 PRINT "(1) = LEGIONS",,,"(2) = NAVY"
590 PRINT "(3) = MERCHANT NAVY",,,"(4) = CRAFT GUILDS"
600 PRINT "(5) = SHIP BUILDING"
610 INPUT A
620 IF A<1 OR A>5 THEN GOTO 560
630 IF A=1 THEN GOTO 680
640 IF A=2 THEN GOTO 710
650 IF A=3 THEN GOTO 740
660 IF A=4 THEN GOTO 770
670 IF A=5 THEN GOTO 800
680 PRINT "{DOWN}LABOUR FOR LEGIONS"
685 REM DOWN CRSR
690 INPUT B:IF B>UL THEN GOTO 680
700 LA=LA+B:UL=UL-B:GOTO 500
710 PRINT "{DOWN}LABOUR FOR NAVY"
720 INPUT B:IF B>UL THEN GOTO 720
725 REM "{DOWN}" IS DOWN CRSR
730 LN=LN+B:UL=UL-B:GOTO 500
740 PRINT "{DOWN}LABOUR FOR MERCHANT NAVY"
750 INPUT B:IF B>UL THEN GOTO 750
760 LM=LM+B:UL=UL-B:GOTO 500
770 PRINT "{DOWN}LABOUR FOR GUILDS"
780 INPUT B:IF B>UL THEN GOTO 780
790 LE=LE+B:UL=UL-B:GOTO 500
800 PRINT "{DOWN}LABOUR FOR SHIPBUILDING"
810 INPUT B:IF B>UL THEN GOTO 810
820 LS=LS+B:UL=UL-B:GOTO 500
830 PRINT "{CLR}":REM CLR SCRN
840 X=1:Y=6:GOSUB 6050
850 PRINT "NO.OF AUXILIA UNITS FOR HIRE =";MA
860 PRINT "{DOWN}NO.OF AUXILIA UNITS HIRED    =";MH
865 REM DOWN CRSR
870 PRINT "{DOWN}DO YOU WISH TO...":GOSUB 5000:GOSUB 6030
880 PRINT "(1) = HIRE AUXILIA UNITS"
890 PRINT "(2) = FIRE AUXILIA UNITS"
900 PRINT "(3) = NONE OF THE ABOVE";
910 INPUT B:IF B<1 OR B>3 THEN GOTO 910
920 IF B=1 THEN GOTO 950
930 IF B=2 THEN GOTO 980
940 IF B=3 THEN RETURN
950 PRINT "{DOWN}HOW MANY UNITS WILL YOU HIRE"
955 REM DOWN CRSR
960 INPUT B:IF B>MA OR B<0 THEN GOTO 950
970 MH=MH+B:MA=MA-B:GOTO 830
980 PRINT "{DOWN}HOW MANY UNITS WILL YOU FIRE"
990 INPUT B:IF B>MH OR B<0 THEN GOTO 990
1000 MH=MH-B:MA=MA+B:MI=B*20:X=INT(100*RND(1)+1)
1010 PRINT"{CLR}{DOWN}{C3}THE AUXILIA YOU HAVE FIRED ARE","THREATENING TO MARCH ON ROLE!!{YEL}"
1015 REM CLR SCRN DOWN CRSR LT RED YELLOW
1020 GOSUB 6000:IF MI<X THEN GOTO 1120
1030 PRINT "{DOWN}{RVS}{5 RIGHT}THE AUXILIA ARE RIOTING!":GOSUB 5200:GOSUB 6000
1035 REM DOWN CRSR REV ON RIGHT CRSR
1040 U=INT(10*RND(1)+1)*1000:V=INT(20*RND(1)+1)*1000:W=INT(10*RND(1)+1)+2
1050 PRINT "{DOWN}THEY HAVE LOOTED YOUR TREASURY OF";U;,"GOLD PIECES!":GOSUB 6010
1060 PRINT "{DOWN}AND PUT";V;"CITIZENS TO THE SWORD!!":GOSUB 6010
1070 PRINT "{DOWN}LABOUR KILLED:-":GOSUB 6010
1080 PRINT "CRAFT GUIDES =";INT(W/3)
1090 PRINT "SHIP BUILDING =";INT(W/3)
1100 TR=TR-U:PO=PO-V:LS=LS-INT(W/3):LE=LE-INT(W/3)
1110 GOSUB 6080:GOTO 830
1120 PRINT "{DOWN}THE AUXILIA UNITS HAVE DECIDED TO"
1130 PRINT "RETURN TO THEIR HOMELANDS PEACEFULLY"
1140 GOSUB 6000:GOTO 830
1150 PRINT "{CLR}":US=LS:REM CLR SCRN
1160 IF US=0 THEN PRINT "{CLR}{DOWN}{2 RIGHT}YOU HAVE NO MORE SHIPS TO BUILD":GOSUB 6010:RETURN
1165 REM CLR SCRN DOWN RIGHT CRSR
1170 PRINT "{CLR}SHIPS TO BUILD =";US
1175 REM CLR SCRN
1180 NW=LN-WS:MW=LM-MS
1190 PRINT "{DOWN}NO.OF CREWS WITHOUT WAR GALLEYS:";NW
1195 REM DOWN CRSR
1200 PRINT "NO.OF CREWS WITHOUT MERCHANT SHIPS:";MW
1210 PRINT "{DOWN}DO YOU WISH TO BUILD....."
1220 PRINT "(1) = WAR GALLEYS",,,"(2) = MERCHANT SHIPS"
1230 PRINT "(3) = NONE";
1240 INPUT B
1250 IF B<0 OR B>3 THEN GOTO 1240
1260 IF B=1 THEN GOTO 1290
1270 IF B=2 THEN GOTO 1320
1280 IF B=3 THEN GOTO 1350
1290 PRINT "{DOWN}HOW MANY WAR GALLEYS WILL YOU BUILD";
1295 REM DOWN CRSR
1300 INPUT B:IF B<0 OR B>US THEN GOTO 1300
1310 US=US-B:WS=WS+B:GOTO 1160
1320 PRINT "{DOWN}HOW MANY MERCHANT SHIPS WILL YOU BUILD";
1330 INPUT B:IF B<0 OR B>US THEN GOTO 1330
1340 US=US-B:MS=MS+B:GOTO 1160
1350 RETURN
1370 IF LN>WS THEN WA=WS
1380 IF WS>=LN THEN WA=LN
1390 IF MS>LM THEN MU=LM
1400 IF LM>=MS THEN MU=MS
1410 PRINT "{CLR}":GOSUB 6020
1415 REM CLR SCRN
1420 X=1:Y=5:GOSUB 6050
1430 PRINT "WAR GALLEYS AVAILABLE =";WA
1440 PRINT "MERCHANT SHIPS AVAILABLE =";MU
1450 PRINT "{DOWN}YOU CAN SEND UP TO";LE;"MERCHANT SHIPS TO","TRADE OVERSEAS"
1460 PRINT "{DOWN}HOW MANY WILL YOU SEND OVERSEAS TO TRADE"
1465 REM DOWN CRSR
1470 INPUT B:IF B<0 OR B>LE THEN GOTO 1470
1480 MO=B:ML=MU-B
1490 PRINT "HOW MANY WAR GALLEYS WILL ESCORT THEM"
1500 INPUT B:IF B<0 OR B>WA THEN GOTO 1500
1510 WO=B:WL=WA-B
1520 RETURN
1530 PRINT "{CLR}":REM CLR SCRN
1540 X=5:Y=6:GOSUB 6050:PRINT "{RVS}{PUR}CHECKING FOR WARS AND REBELLIONS{YEL}"
1545 REM REV ON PURPLE YELLOW
1550 GOSUB 6010
1560 PP=INT(1000*RND(1)+1)+(VF*3)
1570 IF PP>950 THEN X=5:Y=10:GOSUB 6050:VF=VF+5
1580 IFPP>950THENPRINT"THERE ARE NO WARS OR REBELLI9ONS":GOSUB 6020:GOTO 1740
1590 IF PP<175 THEN A$="WAR":B$="WITH CARTHAGE":GOTO 1690
1600 IF PP<350 THEN A$="REBELLION":B$="IN GAUL":GOTO 1700
1610 IF PP<450 THEN A$="REBELLION":B$="IN BRITAIN":GOTO 1700
1620 IF PP<525 THEN A$="WAR":B$="WITH MACEDONIA":GOTO 1710
1630 IF PP<625 THEN A$="WAR":B$="WITH PARTHIA":GOTO 1710
1640 IF PP<675 THEN A$="WAR":B$="WITH GREECE":GOTO 1720
1650 IF PP<750 THEN A$="WAR":B$="WITH PICTS":GOTO 1720
1660 IF PP<875 THEN A$="REBELLION":B$="IN JUDEA":GOTO 1720
1670 IF PP<925 THEN A$="REBELLION":B$="IN SPAIN":GOTO 1730
1680 A$="REBELLION":B$="ON CRETE":GOTO 1730
1690 G=INT(15*RND(1))+5:H=1.8:GOTO 1750
1700 G=INT(12*RND(1))+3:H=1.6:GOTO 1750
1710 G=INT(10*RND(1))+2:H=1.4:GOTO 1750
1720 G=INT(8*RND(1))+1:H=1.2:GOTO 1750
1730 G=INT(4*RND(1))+1:H=1:GOTO 1750
1740 G=INT(3*RND(1))+1:GOSUB 6080:PX=100:FX=100:RETURN
1750 X=6:Y=10:GOSUB 6050
1760 PRINT A$;" BREAKS OUT ";B$:GOSUB 5200
1770 GOSUB 6080:RETURN
1800 IF PP>950 THEN GOTO 2280
1810 ZB=INT(100*RND(1))+1
1820 IF TN<-50 THEN ZD=2:GOTO 1860
1830 IF TN<-45 THEN ZD=1.5:GOTO 1860
1840 IF TN<-40 THEN ZD=1:GOTO 1860
1850 IF TN<-35 THEN ZD=.75
1860 ZC=INT((LA+MH+(WL/3))*ZD/H)
1870 IF ZC>100 THEN ZC=99
1880 PRINT "{CLR}"
1885 REM CLR SCRN
1890 X=8:Y=1:GOSUB 6050:PRINT "YOUR CHANCE TO WIN IS";ZC;"%"
1910 GOSUB 5200:GOSUB 6010
1920 IF ZB=<ZC THEN GOTO 2050
1930 PRINT "{DOWN}{RVS}{WHT}*********** ROME IS DEFEATED ***********{YEL}":GOSUB 5400
1935 REM DOWN CRSR REV ON WHITE YELLOW
1940 WW=INT(10*RND(1)+1*H):IF WW>WL THEN WW=WL
1950 MW=INT(20*RND(1)+7*H):IF MW>ML THEN MW=ML
1960 MX=INT(5*RND(1)+2*H):IF MX>MO THEN MX=MO
1970 WX=INT(3*RND(1)+1*H):IF WX>WO THEN WX=WO
1980 AW=INT(6*RND(1)+1*H):IF AW>LA THEN AW=LA
1990 SW=INT(8*RND(1)+1*H):IF SW>MH THEN SW=MH
2000 FX=INT(20*RND(1)+79)
2010 PX=INT(5*RND(1)+95)
2020 SX=INT(3*RND(1)*H):IF SX>LS THEN SX=LS
2030 EX=INT(3*RND(1)*H):IF EX>LE THEN EX=LE
2040 VF=VF-INT(G*2):GOTO 2160
2050 PRINT "{DOWN}{RVS}{C3}********** ROME IS VICTORIOUS **********{YEL}":GOSUB 5400
2055 REM DOWN CRSR REV ON LT RED YELLOW
2060 WW=INT(5*RND(1)+1*H):IF WW>WL THEN WW=WL
2070 MW=INT(10*RND(1)+3*H):IF MW>ML THEN MW=ML
2080 MX=1:IF MO=0 THEN MX=0
2090 WX=0
2100 AW=INT(2*RND(1)+1*H):IF AW>LA THEN AW=LA
2110 SW=INT(3*RND(1)+1*H):IF SW>MH THEN SW=MH
2120 FX=100
2130 PX=100
2140 SX=0
2150 EX=0:VF=VF+INT(G*1.5)
2160 PRINT:PRINT "{RVS}{RIGHT}{C1}LOSSES{DOWN}{YEL}"
2165 REM REV ON RIGHT CRSR ORANGE DOWN CRSR YELLOW
2170 PRINT WW;"LOCAL WARSHIPS"
2180 PRINT WX;"OVERSEAS WARSHIPS"
2190 PRINT:PRINT MW;"LOCAL MERCHANT SHIPS"
2200 PRINT MX;"OVERSEAS MERCHANT SHIPS"
2210 PRINT:PRINT AW;"LEGIONS"
2220 PRINT SW;"AUXILIA UNITS"
2230 PRINT:PRINT 100-FX;"% OF FOOD STOCKPILE"
2240 PRINT 100-PX;"% OF THE EMPIRE'S POPULATION"
2250 PRINT:PRINT SX;"SHIPBUILDING COHORTS"
2260 PRINT EX;"CRAFTSMAN GUILDS{DOWN}"
2270 PRINT:PRINT:GOSUB 6080
2280 PRINT "{CLR}":X=3:Y=10:GOSUB 6050
2285 REM CLR SCRN
2290 IF ZB<=ZC THEN PRINT "ROME'S TRADE VALUE INCREASES BY";G;"%"
2300 IF ZB<=ZC THEN VL=VL+G
2310 IF ZB>ZC THEN PRINT "ROME'S TRADE VALUE DECREASES BY";G;"%"
2320 IF ZB>ZC THEN VL=VL-G
2330 PRINT:PRINT TAB(8) "TRADE VALUE IS NOW";VL;"%"
2340 GOSUB 6080
2350 WS=WS-WW-WX:LN=LN-WW-WX
2360 MS=MS-MW-MX:LM=LM-MW-MX
2370 LA=LA-AW:MH=MH-SW
2380 FS=INT(FS*FX/100)
2390 PO=INT(PO*PX/100)
2400 LS=LS-SX:LE=LE-EX:RETURN
2410 PRINT "{CLR}":X=7:Y=6:GOSUB 6050
2420 PRINT "{RVS}{WHT}PIRATES ATTACK YOUR SHIPPING{YEL}":GOSUB 5000
2430 RL=INT(50*RND(1)+50)
2435 REM REV ON WHITE YELLOW
2440 RP=INT((RL-WL)/10)
2450 IF RP<0 THEN RP=0
2460 IF RP>ML THEN RP=ML
2470 ML=ML-RP:MS=MS-RP:LM=LM-RP
2480 PRINT:PRINT RP;"LOCAL MERCHANT SHIPS LOST"
2490 RO=INT(50*RND(1)+50)
2500 RQ=INT((RO-WO)/10)
2510 IF RQ<0 THEN RQ=0
2520 IF RQ>MO THEN RQ=MO:MO=MO-RQ:MS=MS-RQ:LM=LM-RQ
2530 PRINT:PRINT RQ;"OVERSEAS MERCHANT SHIPS LOST"
2540 GOSUB 6010:RETURN
2550 PRINT "{CLR}":X=3:Y=1:GOSUB 6050
2560 PRINT "{RVS}{C3}FINANCIAL RETURN{YEL}{DOWN}"
2570 PRINT INT(ML*VL);"= LOCAL TRADE RETURN"
2580 PRINT INT(MO*VL*20);" = OVERSEAS TRADE RETURN"
2590 TR=TR+INT((ML*VL)+(MO*VL*20))
2600 PRINT TR;"= GOLD IN TREASURY"
2610 GOSUB 6020:X=3:Y=9:GOSUB 6050
2620 PRINT "{RVS}{C7}EXPENSES{YEL}":PRINT
2625 REM REV ON LT BLUE YELLOW
2630 PRINT (MH*2500);"FOR";MH;"AUXILIA UNITS"
2640 PRINT LA*500;"FOR";LA;"LEGIONS"
2650 PRINT LN*500;"FOR";LN;"NAVAL CREWS":PRINT "{2 DOWN}"
2660 TR=TR-(LA*500)-(LN*500)-(MH*2500)
2670 PRINT "{RVS}{2 RIGHT}{PUR}FINAL BALANCE{YEL}":PRINT
2675 REM REV ON PURPLE YELLOW
2680 PRINT TR;"GOLD PIECES"
2690 GOSUB 6080:RETURN
2700 FV=INT(40*RND(1))+85
2720 PRINT "{CLR}":X=10:Y=2:GOSUB 6050
2730 PRINT "{RVS}{C6}FEEDING YOUR PEOPLE{DOWN}{YEL}"
2735 REM REV ON :LT GREEN DOWN CRSR YELLOW
2740 PRINT "YOUR";ML;"LOCAL MERCHANT SHIPS"
2750 PRINT "HAVE PURCHASED";INT(ML*FV/100);"FOOD POINTS"
2760 PRINT "{DOWN}PLUS YOUR FOOD STOCKPILE OF"
2770 PRINT FS;"POINTS GIVES YOU";FS+INT(ML*FV/100)"FOOD POINTS"
2780 FS=FS+INT(ML*FV/100)
2790 FS=FS-FR:IF FS<-0 THEN GOTO 2810
2800 PRINT "{2 DOWN}THERE IS PLENTY OF FOOD FOR ALL":GOSUB 6080:RETURN
2810 PRINT "{2 DOWN}{RVS}{5 RIGHT}YOU HVE STARVED THE PEOPLE!!{DOWN}"
2820 PRINT "THEY NEED";ABS(FS);"FOOD POINTS"
2830 GOSUB 6000:TR=TR+(2000*FS)
2840 PRINT "PAY ";-2000*FS;"GOLD PIECES TO IMPORT","GRAIN AT INFLATED PRICES"
2850 VF=VF-(2*FS):FS=0
2860 GOSUB 6080:RETURN
2870 PRINT "{RVS}{CLR}{10 RIGHT}{DOWN}{CYN}POPULARITY REPORT{YEL}":X=1:Y=5:GOSUB 6050
2875 REM REV ON CLR SCRN RIGHT CRSR DOWN CRSR CYAN YELLOW
2890 IF VF<0 THEN GOTO 3010
2990 PRINT "{6 RIGHT}YOUR RULE IS STILL POPULAR"
3000 GOSUB 6080:RETURN
3010 PRINT "IMPERIAL INTELLIGENCE REPORTS"
3020 PRINT "THAT YOUR RULE IS NOT POPULAR"
3030 PRINT"{DOWN}YOU NEED";ABS(VF);"POINTS TO BECOME POPULAR":IF TR<1000 THEN RETURN
3035 REM DOWN CRSR
3040 PRINT "{DOWN}HOW MUCH WILL YOU SPEND TO APPEASE"
3050 PRINT "ROME AND ITS CITIZENS";
3060 INPUT A
3070 IF A=0 THEN RETURN
3080 A=INT(A/1000)
3090 VF=VF+A
3100 RETURN
3110 PO=INT(PO*1.07):FR=INT(PO/1000):WW=0:MW=0:WX=0:MX=0:AW=0:SW=0:FX=0
3120 MA=MA+INT(6*RND(1)+1):PX=0:SX=0:EX=0
3130 TN=TN+1:UL=INT(PO/2500)
3140 SC=INT(((LA+LN+LM+LE+LS)*100)+(TR/2)+(PO/50)/(100-TN))
3145 PRINT "{CLR}{3 DOWN}{7 RIGHT}YOUR SCORE =";SC:GOSUB 6080
3147 REM CLR SCRN DOWN CRSR RIGHT CRSR
3150 IF TN>-36 THEN GOTO 3170
3160 X=1:Y=10:GOSUB 6050:GOTO 40
3170 FOR TD=1 TO 1500:NEXT:PRINT "{CLR}":REM CLR SCRN
3175 X=12:Y=12:GOSUB 6050
3180 PRINT "THE GAME IS OVER!!!"
3185 X=8:Y=14:GOSUB 6050
3190 PRINT "YOU SCORED";SC;"POINTS"
3200 END
5000 REM SOUND
5010 POKE AD,17:POKE SR,132:POKE WF,17
5020 POKE VO,15:POKE HF,68:POKE LF,149
5030 FOR TD=1 TO 350:NEXT
5040 POKE VO,0:POKE WF,0
5050 RETURN
5200 REM SOUND
5210 POKE AD,17:POKE SR,192:POKE WF,33
5220 POKE VO,15
5230 POKE HF,5:POKE LF,155:FOR TD=1 TO 1000:NEXT
5240 POKE WF,0:POKE VO,0:RETURN
5400 REM SOUND
5410 POKE VO,15:POKE AD,18:POKE SR,64:POKE WF,17
5420 FOR TT=1 TO 3:POKE HF,102:POKE LF,194:FOR TD=1 TO 150:NEXT
5430 POKE HF,0:POKE LF,0:FOR TD=1 TO 150:NEXT:NEXT
5440 POKE VO,0:POKE WF,0:RETURN
5800 PRINT "{CLR}{RVS}{CYN}SENATE REPORT:YEAR";ABS(TN);"B.C.{5 LEFT} "
5805 PRINT "{HOME}" TAB(26) "{RVS}{CYN}              ":REM 14 SPACES
5810 PRINT "{DOWN}{YEL}POPULATION",PO
5820 PRINT "ARMY LEGIONS",LA
5830 PRINT "AUXILIA UNITS",MH
5840 PRINT "{DOWN}WAR GALLEYS",WS
5850 PRINT "NAVY CREWS",LN
5860 PRINT "{DOWN}MERCHANT SHIPS",MS
5870 PRINT "MERCHANT SHIP CREWS",LM
5880 PRINT "{DOWN}SHIPBUILDING COHORTS";LS
5890 PRINT "CRAFTSMAN GUILDS",LE
5900 PRINT "{DOWN}UNALLOCATED LABOUR",UL
5910 PRINT "{DOWN}GOLD IN TREASURY",TR
5920 PRINT "FOOD STOCKPILE",FS
5930 PRINT "FOOD REQUIRED",FR
5950 GOSUB 6080:RETURN
6000 FOR TD=1 TO 1000:NEXT TD
6010 FOR TD=1 TO 500:NEXT TD
6020 FOR TD=1 TO 475:NEXT TD
6030 FOR TD=1 TO 25:NEXT TD
6040 RETURN
6050 PRINT LEFT$(X$,X);LEFT$(Y$,Y);
6070 RETURN
6080 X=10:Y=22:GOSUB 6050:PRINT "{RVS}{C5}HIT ANY KEY TO CONTINUE{YEL}":GOSUB 5000
6090 GET A$
6100 IF A$="" THEN GOTO 6090
6110 RETURN
7000 POKE 53280,6:POKE 53281,7
7010 XX$="              ROMAN EMPIRE"
7020 GX=26
7030 FOR XG=1 TO 26
7040 G2$=MID$(XX$,GX,XG)
7050 PRINT "{HOME}{8 DOWN}{RED}";G2$;
7060 GOSUB 7400
7100 GX=GX-1
7110 NEXT
7120 POKE VO,0:POKE WF,0:POKE HF,0:POKE LF,0
7130 FOR TD=1 TO 1000:NEXT
7140 PRINT "{CLR}":POKE 53280,3:POKE 53281,6
7150 RETURN
7400 POKE VO,15:POKE AD,17:POKE SR,18:POKE WF,17
7410 POKE HF,INT(100*RND(1)+75)
7420 POKE LF,INT(100*RND(1)+120)
7430 FOR TD=1 TO 50:NEXT
7450 RETURN
