5 REM SPACE RACE
10 PRINT "{CLR}":GOSUB 600:REM CLR SCRN
15 X=20
50 A=A*4
60 PRINT "{CLR}"
70 POKE 53280,0:POKE 53281,0
100 REM ROUTINE
110 PRINT
120 POKE 1024+X,22:POKE 55296+X,7
130 C=C+1:IF C>INT(RND(1)*7)+4+A THEN 200
135 POKE 1024+X,96
140 GET A$
150 IF A$="Z" THEN X=X-1
160 IF A$="M" THEN X=X+1
165 POKE 1024+X,22:POKE 55296+X,7
170 FOR T=1 TO A:NEXT T
175 GOSUB 300
180 GOTO 100
200 C=0:D=INT(RND(1)*20)+10
205 E=E+1:IF E=31 THEN PRINT "{RVS}{HOME}GAME OVER":FOR T=1 TO 2000:NEXT T:GOSUB 500
206 REM REV ON HOME
210 PRINT TAB(D) "{WHT}m   n":REM WHITE
212 PRINT TAB(D) " {RVS}{BLU}   {OFF}":REM REV ON BLUE THREE SPACES REV OFF
215 PRINT TAB(D) "{WHT}n   m":REM WHITE
220 GOTO 100
300 IF PEEK(1024+X-1)=160 OR PEEK(1024+X+1)=160 THEN SC=SC+10*E
320 RETURN
500 POKE 53280,2:POKE 53281,3:PRINT "{CLR}"
510 PRINT TAB(10) "{RED}{7 DOWN}YOUR SCORE WAS";SC
520 IF SC>HS THEN HS=SC
530 PRINT TAB(4) "{BLU}{2 DOWN}THE BEST SCORE SO FAR IS";HS
540 FOR TD=1 TO 1000:NEXT
550 PRINT TAB(4) "{PUR}{2 DOWN}DO YOU WANT ANOTHER GAME (Y/N)";
560 INPUT A$
570 IF A$="Y" THEN SC=0:E=0:GOTO 10
580 END
600 PRINT "{CLR}":POKE 53280,2:POKE 53281,6
610 PRINT TAB(14) "{8 DOWN}{RVS}{C1}SPACE RACE"
620 FOR TD=1 TO 1500:NEXT TD
630 PRINT TAB(7) "{2 DOWN}{CYN}CHOOSE FLIGHT SPEED (1-4)";
640 INPUT A
650 RETURN
