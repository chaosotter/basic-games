10 REM *** BEYOND BAGELS ***
15 RANDOMIZE TIMER
20 REM *** G CONTROLS THE MAXIMUM NUMBER OF GUESSES
30 G = 20
40 PRINT "WELCOME TO   *** BEYOND BAGELS ***"
50 PRINT
60 PRINT "RULES (1=YES 0=NO)";
70 INPUT X
80 PRINT
90 IF X <> 1 THEN 310
100 PRINT "   I'LL THINK OF A THREE DIGIT NUMBER (LIKE 532)"
110 PRINT "AND YOU HAVE"; G; "TRIES TO GUESS IT.  AFTER YOU TELL ME"
120 PRINT "YOUR GUESS, I'LL PRINT YOUR SCORE FOR THAT GUESS."
130 PRINT
140 PRINT "YOU GET 1 POINT FOR EACH DIGIT THAT'S ALSO IN MY NUMBER,"
150 PRINT "AND ANOTHER POINT FOR EACH CORRECT DIGIT THAT'S ALSO"
160 PRINT "IN THE CORRECT PLACE."
170 PRINT
180 PRINT "IF MY NUMBER IS   532"
190 PRINT "AND YOU GUESS     421"
200 PRINT "YOU GET 1 POINT (FOR THE 2)"
210 PRINT
220 PRINT "IF MY NUMBER IS   689"
230 PRINT "AND YOU GUESS     692"
240 PRINT "YOU GET 3 POINTS (2 BECAUSE OF THE 6, AND"
250 PRINT "THE 9 ADDS THE OTHER POINT)"
260 PRINT
270 PRINT "BUT BE CAREFUL!  SOME SCORES (LIKE 3 POINTS)"
280 PRINT "CAN BE MADE IN MORE THAN ONE WAY !!!"
290 PRINT
300 PRINT "HERE WE GO..."
310 DIM A(3), B(3)
320 A(1) = INT(RND(1) * 9) + 1
330 A(2) = INT(RND(1) * 10)
340 IF A(2) = A(1) THEN 330
350 A(3) = INT(RND(1) * 10)
360 IF A(3) = A(2) THEN 350
370 IF A(3) = A(1) THEN 350
380 PRINT "I'VE PICKED MY NUMBER"
390 PRINT
400 FOR G1 = 1 TO G
410 PRINT
420 PRINT "GUESS #"; G1;
430 INPUT X
440 IF X >= 100 AND X <= 999 THEN 470
450 PRINT "HINT:  MY NUMBER HAS THREE DIGITS"
460 GOTO 410
470 P = 0
480 FOR I = 1 TO 3
490 B(I) = INT(X / 100)
500 X = 10 * (X - 100 * B(I))
510 NEXT I
520 IF P = 6 THEN 750
530 IF B(1) <> B(2) AND B(1) <> B(3) AND B(2) <> B(3) THEN 560
540 PRINT "HINT:  IN MY NUMBER, ALL THE DIGITS ARE DIFFERENT"
550 GOTO 410
560 FOR I = 1 TO 3
570 FOR J = 1 TO 3
580 IF B(I) <> A(J) THEN 620
590 P = P + 1
600 IF I <> J THEN 620
610 P = P + 1
620 NEXT J
630 NEXT I
640 IF P = 6 THEN 750
650 IF P <> 1 THEN 680
660 PRINT P; "POINT FOR THIS ONE"
670 GOTO 690
680 PRINT P; "POINTS FOR THIS ONE"
690 NEXT G1
700 PRINT
710 PRINT
720 PRINT "SORRY YOU DIDN'T GUESS IT."
730 PRINT "MY NUMBER WAS "; A(1); A(2); A(3)
740 GOTO 820
750 PRINT
760 PRINT
770 PRINT "YOU GUESSED IT IN "; G1; " GUESSES !!!";
780 FOR I = 1 TO G / 2 - G1
790 PRINT "";
800 NEXT I
810 PRINT
820 PRINT "PLAY AGAIN (1=YES 0=NO)";
830 INPUT X
840 IF X = 1 THEN 290
850 PRINT "BYE!"
860 END

