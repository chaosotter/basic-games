10 REM *** BUTTON, BUTTON, WHO'S GOT THE BUTTON? ***
15 REM *** WRITTEN BY * DAVE KAUFMAN * AUGUST 1973 ***
20 REM *** PEOPLE'S COMPUTER COMPANY, MENLO PARK, CALIFORNIA ***
25 REM ****************************** REM
30 REM FNM COMPUTES X MODULAR 7
35 REM FNN COMPUTES THE NEXT ONE TO GET THE BUTTON
40 DEF FNM(X) = X MOD 7
45 DEF FNN(X) = FNM(X + SGN(RND(1) - .5))
50 GOSUB 1000: RANDOMIZE TIMER
100 REM *** GAME STARTS HERE ***
110 REM B  THE ONE WHO HAS THE BUTTON
120 REM L  THE LAST ONE WHO HAD IT
130 B = INT(RND(1) * 6) + 2
140 L = 8
150 PRINT
160 PRINT
170 REM *** GUESSING STARTS HERE ***
180 REM G  PLAYER'S GUESS
190 PRINT "WHO DO YOU GUESS HAS IT";
200 INPUT G
210 IF G = 0 THEN 440
220 IF G = B THEN 430
230 IF G = L THEN 360
240 IF G = FNM(B + 1) OR G = FNM(B - 1) THEN 340
250 IF G > 0 AND G < 8 THEN 290
260 PRINT "SILLY - THERE'S NO ONE HERE BY THAT NUMBER"
270 PRINT "TRY AGAIN ..."
280 GOTO 190
290 PRINT G; ":  'WHO, ME?'"
300 PRINT
310 PRINT
320 PRINT "WHOEVER HAS IT, KEEPS IT"
330 GOTO 170
340 PRINT G; ":  'MY NEIGHBOR HAS IT'"
350 GOTO 370
360 PRINT G; ":  'I HAD IT LAST TIME'"
370 L = B
380 B = FNN(B)
390 PRINT
400 PRINT
410 PRINT "...BUT WHOEVER HAS IT, PASSES IT"
420 GOTO 170
430 PRINT G; ":  'RIGHT YOU ARE - LUCKY!'"
440 REM *** AGAIN? ***
450 PRINT
460 PRINT "AGAIN (1=YES, 0=NO)";
470 INPUT G
480 IF G = 0 THEN 510
490 PRINT "*** NEW GAME ***"
500 GOTO 100
510 REM *** END PROGRAM ***
520 STOP
1000 REM *** THE INTRO ***
1010 PRINT
1020 PRINT
1030 PRINT "BUTTON, BUTTON, WHO'S GOT THE BUTTON?"
1040 PRINT
1050 PRINT
1060 PRINT "SEVEN FRIENDS ARE SITTING IN A CIRCLE"
1070 PRINT "AND YOU'RE IN THE CENTER"
1080 PRINT
1090 PRINT
1100 PRINT "      1"
1110 PRINT
1120 PRINT " 7        2"
1130 PRINT
1140 PRINT "   YOU'RE"
1150 PRINT "6    IT     3"
1160 PRINT
1170 PRINT "   5     4"
1180 PRINT
1190 PRINT
1200 PRINT "SOMEONE HAS THE BUTTON AND YOU HAVE TO GUESS WHO"
1210 PRINT
1220 PRINT "HE CAN PASS THE BUTTON (IF HE WANTS TO)"
1230 PRINT "BUT ONLY TO SOMEONE HE'S SITTING NEXT TO"
1240 PRINT
1250 PRINT "IF YOU WANT TO STOP, TYPE 0 (ZERO)"
1260 PRINT
1270 PRINT "GOOD LUCK   !!!"
1280 RETURN
1290 END

