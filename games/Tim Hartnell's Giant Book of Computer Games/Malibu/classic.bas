5 DIM H(3), C(3)
10 REM MALIBU
20 GOSUB 680
30 REM *****************************
40 REM *** THE MAIN GAME ROUTINE ***
50 FOR T = 1 TO 5
60 CLS
70 PRINT: PRINT: PRINT "THIS IS ROUND"; T
80 IF CS * HS <> O THEN PRINT A$; ":"; HS; " ME:"; CS
90 GOSUB 840
100 PRINT: PRINT "FIRST I'LL ROLL THREE DICE FOR"
110 PRINT "MYSELF ... STAND BY ... "
120 GOSUB 840
130 FOR Z = l TO 3: C(Z) = 0: H(Z) = 0: NEXT Z
140 GOSUB 850
150 PRINT: PRINT "AND NOW IT'S TIME TO ROLL FOR YOU"
160 GOSUB 850
170 PRINT: PRINT "I ROLLED"; C(1); C(2); C(3)
180 PRINT: PRINT "YOU ROLLED"; H(1); H(2); H(3)
190 REM *** ASSESS RESULT OF ROLLS ***
200 HT = H(1) + H(2) + H(3)
210 CT = C(1) + C(2) + C(3)
220 IF (C(1) = C(2) OR C(2) = C(3) OR C(1) = C(3)) AND CT = 13 THEN PRINT "THAT'S SOUGH FOR ME!": CS = CS + 10: HS = HS - 10
230 IF (H(1) = H(2) OR H(2) = H(3) OR H(1) = H(3)) AND HT = 13 THEN PRINT "THAT'S SOUGH FOR YOU!": HS = HS + 10: CS = CS - 10
240 IF HT = 15 OR HT = 6 THEN HS = HS + 4: CS = CS - 4: PRINT "**EASY RIDER FOR YOU**"
250 IF CT = 15 OR CT = 6 THEN CS = CS + 4: HS = HS - 4: PRINT "**EASY RIDER FOR ME**"
260 IF CT = 9 OR CT = 12 THEN CS = CS + 3: HS = HS - 3: PRINT ">> I CRACK A STRAIGHT ROAD <<"
270 IF HT = 9 OR HT = 12 THEN BS = HS + 3: CS = CS - 3: PRINT ">> YOU CRACK A STRAIGHT ROAD <<"
280 IF C(1) = C(2) AND C(2) = C(3) THEN PRINT "A TRIPLE CROWN FOR ME!": CS = CS + 5: HS = HS - 5: GOTO 300
290 IF C(1) = C(2) OR C(2) = C(3) OR C(1) = C(3) THEN PRINT "$$ TWO OF A KIND FOR ME $$": CS = CS + 3: HS = HS - 3
300 IF H(1) = H(2) AND H(2) = H(3) THEN PRINT "A TRIPLE CROWN FOR YOU!": HS = HS + 5: CS = CS - 5: GOTO 320
310 IF H(1) = H(2) OR H(2) = H(3) OR H(1) = H(3) THEN PRINT "$$ TWO OF A KIND FOR YOU $$": HS = HS + 3: CS = CS - 3
320 IF HT = 13 THEN PRINT "FOR YOU...LUCKY JOE!": HS = HS + 6: CS = CS - 6
330 IF CT = 13 THEN PRINT "FOR ME...LUCKY JOE!": CS = CS + 6: HS = HS - 6
340 IF HT = 3 THEN PRINT "LOW AND MEAN...FOR YOU!": HS = HS + 7: CS = CS - 7
350 IF CT = 3 THEN PRINT "LOW AND MEAN...FOR ME!": CS = CS + 7: HS = HS - 7
360 IF HT = 18 THEN PRINT "HIGH ROLLER FOR THE HUMAN...": HS = HS + 12: CS = CS - 12
370 IF CT = 18 THEN PRINT "HIGH ROLLER FOR THE MACHINE...": CS = CS + 12: HS = HS - 12
380 IF HT = 7 THEN PRINT "YOU TRIPPED A DREADED SEVEN": HS = HS - 2: CS = CS + 2
390 IF CT = 7 THEN PRINT "I TRIPPED A DREADED SEVEN": CS = CS - 2: HS = HS + 2
400 REM *** PRINT OUT SCORES ***
410 PRINT: PRINT
420 IF T < 5 THEN PRINT ">>> AFTER ROUND"; T; "THE SCORES ARE:": PRINT ">>> "; A$; ":"; HS; " COMPUTER:"; CS
430 GOSUB 840: GOSUB 840
440 REM ** CHECK BOTH STILL IN GAME **
450 IF CS < 1 OR HS < 1 THEN T = 5
460 NEXT T
470 REM *** RESULT OF FIVE ROUNDS ***
480 PRINT: PRINT "WELL,"; A$; ", THAT'S THE END OF"
490 PRINT "OUR LITTLE GAME OF MALIBU...."
500 PRINT: PRINT A$; "'S FINAL SCORE IS"; HS
510 PRINT "AND MINE IS"; CS
520 GOSUB 840
530 PRINT
540 IF CS > HS THEN PRINT "SO I'M THE WINNER!!!"
550 IF HS > CS THEN PRINT "SO "; A$; " TAKES THE WINNER'S TROPHY!!"
560 IF CS = HS THEN PRINT "IT LOOKS LIKE A DEAD HEAT...."
570 GOSUB 840: GOSUB 840
580 PRINT: PRINT "DO YOU WANT ANOTHER GAME, "; A$; " (Y OR N)?"
590 PRINT
600 F$ = INKEY$
610 IF F$ <> "y" AND F$ <> "Y" AND F$ <> "n" AND F$ <> "N" THEN 600
620 IF F$ = "N" OR F$ = "n" THEN PRINT "OK, THANKS FOR THE GAME, "; A$: PRINT "SEE YOU AGAIN SOME TIME": END
630 PRINT "OK, "; A$; ", STAND BY..."
640 GOSUB 840
650 GOSUB 770
660 GOTO 50
670 REM *********************
680 REM *****INITIALISE******
690 RANDOMIZE VAL(RIGHT$(TIME$, 2))
700 CLS
720 PRINT: PRINT: PRINT "WELCOME TO THE GAME OF MALIBU"
730 PRINT "WHICH IS PLAYED WITH THREE DICE..."
740 GOSUB 840
750 PRINT: INPUT "WHAT IS YOUR NAME"; A$
760 GOSUB 840
770 CLS: PRINT
780 HS = 50: CS = 50
790 PRINT "OK, "; A$; ", PRESS THE SPACE"
800 PRINT "BAR WHEN YOU'RE READY TO PLAY"
810 IF INKEY$ = "" THEN 810
820 RETURN
830 REM *** DELAY SUBROUTINE ***
840 IF INKEY$ = "" THEN 840
845 RETURN
850 REM *** REM DICE ROLL ***
860 FOR Z = 1 TO 3
870 PRINT "...ROLLING DIE"; Z;
880 FOR I = 1 TO 800: NEXT I
890 K = INT(RND(1) * 6) + 1
900 PRINT "WHICH CAME UP"; K
910 PRINT "---------------------------------------"
920 IF C(Z) = 0 THEN C(Z) = K ELSE H(Z) = K
930 NEXT Z
940 GOSUB 840
950 RETURN

