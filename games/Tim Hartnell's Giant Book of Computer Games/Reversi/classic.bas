5 RANDOMIZE TIMER: DIM A(10, 10)
6 WIDTH 40
10 REM REVERSI
20 GOTO 740
30 PRINT "MY MOVE..."
40 S = O: T = X: H = 0
50 FOR A = 2 TO 9: FOR B = 2 TO 9
60 IF A(A, B) <> 46 THEN 210
70 Q = 0
80 FOR C = -1 TO 1: FOR D = -1 TO 1
90 K = 0: F = A: G = B
100 IF A(F + C, G + D) <> S THEN 130
110 K = K + 1: F = F + C: G = G + D
120 GOTO 100
130 IF A(F + C, G + D) <> T THEN 150
140 Q = Q + K
150 NEXT D: NEXT C
160 IF A = 2 OR A = 9 OR B = 2 OR B = 9 THEN Q = Q * 2
170 IF A = 3 OR A = 8 OR B = 3 OR B = 8 THEN Q = Q / 2
180 IF (A = 2 OR A = 9) AND (B = 3 OR B = 8) OR (A = 3 OR A = 8) AND (B = 2 OR B = 9) THEN Q = Q / 2
190 IF Q < H OR (RND < .3 AND Q = H) THEN 210
200 H = Q: M = A: N = B
210 NEXT B: NEXT A
220 IF H = 0 AND R = 0 THEN 690
230 IF H = 0 THEN 250
240 GOSUB 580
250 GOSUB 370
260 INPUT "ENTER YOUR MOVE "; R
270 REM ENTER 0 TO PASS
280 S = X: T = O: REM LETTER 0
290 IF R = 0 THEN 350
300 IF R < 11 OR R > 88 THEN 260
310 R = R + 11
320 M = INT(R / 10)
330 N = R - 10 * M
340 GOSUB 580
350 GOSUB 370
360 GOTO 30
370 REM PRINT BOARD
380 C = 0: H = 0
390 CLS: REM OR 'HOME'
400 PRINT
410 PRINT "     COMPUTER IS X"
420 PRINT "       HUMAN IS O"
430 PRINT
440 PRINT TAB(5); "1 2 3 4 5 6 7 8"
450 FOR B = 2 TO 9: PRINT B - 1; " ";
460 FOR D = 2 TO 9
470 PRINT CHR$(A(B, D)); " ";
480 IF A(B, D) = X THEN C = C + 1
490 IF A(B, D) = O THEN H = H + 1
500 NEXT D
510 PRINT B - 1
520 NEXT B
530 PRINT TAB(5); "1 2 3 4 5 6 7 8"
540 PRINT: PRINT
550 PRINT "Computer:"; C
560 PRINT: PRINT "Human:"; H
570 RETURN
580 FOR C = -1 TO 1: FOR D = -1 TO 1
590 F = M: G = N
600 IF A(F + C, G + D) <> S THEN 630
610 F = F + C: G = G + D
620 GOTO 600
630 IF A(F + C, G + D) <> T THEN 670
640 A(F, G) = T
650 IF M = F AND N = G THEN 670
660 F = F - C: G = G - D: GOTO 640
670 NEXT D: NEXT C
680 RETURN
690 GOSUB 370
700 IF C > H THEN PRINT "I'M THE CHAMP!"
710 IF H > C THEN PRINT "YOU'RE THE CHAMP!"
720 IF H = C THEN PRINT "IT'S A DRAW!"
730 END
740 CLS
750 X = ASC("X"): O = ASC("O"): REM LETTER O NOT ZERO
770 FOR B = 1 TO 10: FOR C = 1 TO 10
780 IF B <> 1 AND C <> 1 AND B <> 10 AND C <> 10 THEN A(B, C) = ASC(".")
790 NEXT C: NEXT B
800 A(5, 5) = X: A(6, 6) = X
810 A(6, 5) = O: A(5, 6) = O
820 P = 0
830 PRINT "DO YOU WANT THE FIRST MOVE"
840 PRINT , "Y OR N"
850 INPUT W$
860 GOSUB 370
870 IF W$ = "Y" OR W$ = "y" THEN GOTO 260
880 GOTO 30

